COMMON-POSTING-BAL-PROC.                                         
                                                                 
    IF  CASH-ADVANCE    OR CASH-ADVANCE-REVERSAL OR              
        CASH-ADJUSTMENT OR RETAIL-PURCHASE       OR              
        RETURNED-ITEM   OR RETAIL-ADJUSTMENT                     
        IF  CMT-ITEM-PRICE OR CMT-LARGE-ITEM-PRICE OR            
            CMT-CASH-ITEM-PRICE                                  
            GO TO CPBP-EXIT                                      
        END-IF                                                   
    END-IF.                                                      
                                                                 
    IF  WS-ABP-TRANS-THRU                                        
        PERFORM COMMON-ABP-BAL-POST     THRU CABP-EXIT           
    ELSE                                                         
        IF ( RETAIL-TRANSACTIONS )        AND                    
           ( NOT DISPUTED-TRANSACTION )   AND                    
           ( NOT RETAIL-PAYMENT )         AND                    
           ( NOT RETAIL-PAYMENT-REVERSAL )                       
            PERFORM COMMON-RTL-BAL-POST    THRU CRBP-EXIT        
        ELSE                                                     
           IF ((CASH-TRANSACTIONS)          AND                  
               (NOT DISPUTED-TRANSACTION)   AND                  
               (NOT CASH-PAYMENT)           AND                  
               (NOT CASH-PAYMENT-REVERSAL)) OR                   
               (CREDIT-BALANCE-REFUND)                           
               PERFORM COMMON-CSH-BAL-POST THRU CCBP-EXIT        
           END-IF                                                
        END-IF                                                   
    END-IF.                                                      
                                                                 
    PERFORM COMMON-GENERAL-BAL-POST     THRU CGBP-EXIT.          
                                                                 
CPBP-EXIT.                                                       
    EXIT.                                                        
                                                                 
COMMON-ABP-BAL-POST.                                             
                                                                 
    EVALUATE TRUE                                                
                                                                 
       WHEN  RETAIL-INTR-DB-ADJUST                               
             MOVE +5120          TO CPS422-FUNCTION-CODE         
             PERFORM ABPS-MAIN-FUNCTION THRU ABPS-MF-XIT         
       WHEN  RETAIL-INTR-CR-ADJUST                               
             MOVE +5120          TO CPS422-FUNCTION-CODE         
             PERFORM ABPS-MAIN-FUNCTION THRU ABPS-MF-XIT         
       WHEN  RETAIL-SVC-CHRG-DB-ADJUST                           
             MOVE +5090          TO CPS422-FUNCTION-CODE         
             PERFORM ABPS-MAIN-FUNCTION THRU ABPS-MF-XIT         
       WHEN  RETAIL-SVC-CHRG-CR-ADJUST                           
             MOVE +5090          TO CPS422-FUNCTION-CODE         
             PERFORM ABPS-MAIN-FUNCTION THRU ABPS-MF-XIT         
       WHEN  RETAIL-PURCHASE                                     
             MOVE +5020          TO CPS422-FUNCTION-CODE         
             PERFORM ABPS-MAIN-FUNCTION THRU ABPS-MF-XIT         
       WHEN  RETURNED-ITEM                                       
             MOVE +5030          TO CPS422-FUNCTION-CODE         
             PERFORM ABPS-MAIN-FUNCTION THRU ABPS-MF-XIT         
       WHEN  RETAIL-DEBIT-ADJUST                                 
             MOVE +5060          TO CPS422-FUNCTION-CODE         
             PERFORM ABPS-MAIN-FUNCTION THRU ABPS-MF-XIT         
       WHEN  RETAIL-CREDIT-ADJUST                                
             MOVE +5060          TO CPS422-FUNCTION-CODE         
             PERFORM ABPS-MAIN-FUNCTION THRU ABPS-MF-XIT         
       WHEN  OTHER                                               
             CONTINUE                                            
    END-EVALUATE.                                                
                                                                 
CABP-EXIT.                                                       
    EXIT.                                                        
                                                                 
COMMON-RTL-BAL-POST.                                             
                                                                 
    EVALUATE TRUE                                                
                                                                 
      WHEN  RETAIL-INTR-DB-ADJUST                                
            ADD CMT-AMNT TO CM-RTL-ICTD                          
      WHEN  RETAIL-INTR-CR-ADJUST                                
            IF  WS-PRE-STMT-FLAG = 'Y'                           
                SUBTRACT CMT-AMNT FROM CM-RTL-IBNP               
            ELSE                                                 
                SUBTRACT CMT-AMNT FROM CM-RTL-ICTD               
            END-IF                                               
      WHEN  RETAIL-SVC-CHRG-DB-ADJUST                            
            ADD CMT-AMNT TO CM-RTL-SVC-CTD                       
      WHEN  RETAIL-SVC-CHRG-CR-ADJUST                            
            IF  WS-PRE-STMT-FLAG = 'Y'                           
                SUBTRACT CMT-AMNT FROM CM-RTL-SVC-BNP            
            ELSE                                                 
                SUBTRACT CMT-AMNT FROM CM-RTL-SVC-CTD            
            END-IF                                               
      WHEN  RETAIL-PURCHASE                                      
            ADD CMT-AMNT TO CM-RTL-CURR-MON-BAL                  
      WHEN  RETAIL-DEBIT-ADJUST                                  
            ADD CMT-AMNT TO CM-RTL-CURR-MON-BAL                  
      WHEN  RETURNED-ITEM                                        
            IF  WS-PRE-STMT-FLAG = 'Y'                           
                SUBTRACT CMT-AMNT FROM CM-RTL-PRIN-STMT          
            ELSE                                                 
                SUBTRACT CMT-AMNT FROM CM-RTL-CURR-MON-BAL       
            END-IF                                               
      WHEN  RETAIL-CREDIT-ADJUST                                 
            IF  WS-PRE-STMT-FLAG = 'Y'                           
                SUBTRACT CMT-AMNT FROM CM-RTL-PRIN-STMT          
            ELSE                                                 
                SUBTRACT CMT-AMNT FROM CM-RTL-CURR-MON-BAL       
            END-IF                                               
      WHEN  MISC-FEE-DEBIT                                       
            IF  CMT-TRANS-CODE = WSSC-AT-TC-MEM-FEE              
                ADD CMT-AMNT TO CM-RTL-MEMBER-CTD                
            ELSE                                                 
                ADD CMT-AMNT TO CM-RTL-MISC-CTD                  
            END-IF                                               
      WHEN  MISC-FEE-CREDIT                                      
            IF  WS-PRE-STMT-FLAG = 'Y'                           
                IF  CMT-TRANS-CODE = WSSC-IN-TC-INT-MEM-FEE-CR   
                    SUBTRACT CMT-AMNT FROM CM-RTL-MEMBER-BNP     
                ELSE                                             
                    SUBTRACT CMT-AMNT FROM CM-RTL-MISC-FEES      
                END-IF                                           
            ELSE                                                 
                IF  CMT-TRANS-CODE = WSSC-IN-TC-INT-MEM-FEE-CR   
                    SUBTRACT CMT-AMNT FROM CM-RTL-MEMBER-CTD     
                ELSE                                             
                    SUBTRACT CMT-AMNT FROM CM-RTL-MISC-CTD       
                END-IF                                           
            END-IF                                               
      WHEN  INSURANCE-PREMIUM                                    
            ADD CMT-AMNT               TO CM-RTL-INSUR-CTD       
      WHEN  INSURANCE-PREMIUM-REFUND                             
            IF  WS-PRE-STMT-FLAG = 'Y'                           
                SUBTRACT CMT-AMNT FROM CM-RTL-INSUR-BNP          
            ELSE                                                 
                SUBTRACT CMT-AMNT FROM CM-RTL-INSUR-CTD          
            END-IF                                               
      WHEN  OTHER                                                
            CONTINUE                                             
                                                                 
    END-EVALUATE.                                                
                                                                 
CRBP-EXIT.                                                       
    EXIT.                                                        
                                                                 
COMMON-CSH-BAL-POST.                                             
                                                                 
    EVALUATE TRUE                                                
      WHEN  CASH-INTR-DB-ADJUST                                  
            ADD CMT-AMNT TO CM-CASH-ICTD                         
      WHEN  CASH-INTR-CR-ADJUST                                  
            IF  WS-PRE-STMT-FLAG = 'Y'                           
                SUBTRACT CMT-AMNT FROM CM-CASH-IBNP              
            ELSE                                                 
                SUBTRACT CMT-AMNT FROM CM-CASH-ICTD              
            END-IF                                               
      WHEN  CASH-SVC-CHRG-DB-ADJUST                              
            ADD CMT-AMNT TO CM-CASH-SVC-CTD                      
      WHEN  CASH-ADV-PER-FEE                                     
            ADD CMT-AMNT TO CM-CASH-SVC-CTD                      
      WHEN  CASH-SVC-CHRG-CR-ADJUST                              
            IF  WS-PRE-STMT-FLAG = 'Y'                           
                SUBTRACT CMT-AMNT FROM CM-CASH-SVC-BNP           
            ELSE                                                 
                SUBTRACT CMT-AMNT FROM CM-CASH-SVC-CTD           
            END-IF                                               
      WHEN  CASH-ADV-PER-FEE-REVERSAL                            
            IF  WS-PRE-STMT-FLAG = 'Y'                           
                SUBTRACT CMT-AMNT FROM CM-CASH-SVC-BNP           
            ELSE                                                 
                SUBTRACT CMT-AMNT FROM CM-CASH-SVC-CTD           
            END-IF                                               
      WHEN  CASH-ADVANCE                                         
            ADD CMT-AMNT TO CM-CASH-CURR-MON-BAL                 
      WHEN  CASH-DEBIT-ADJUST                                    
            ADD CMT-AMNT TO CM-CASH-CURR-MON-BAL                 
      WHEN  CASH-ADVANCE-REVERSAL                                
            IF  WS-PRE-STMT-FLAG = 'Y'                           
                SUBTRACT CMT-AMNT FROM CM-CASH-PRIN-STMT         
            ELSE                                                 
                SUBTRACT CMT-AMNT FROM CM-CASH-CURR-MON-BAL      
            END-IF                                               
      WHEN  CASH-CREDIT-ADJUST                                   
            IF  WS-PRE-STMT-FLAG = 'Y'                           
                SUBTRACT CMT-AMNT FROM CM-CASH-PRIN-STMT         
            ELSE                                                 
                SUBTRACT CMT-AMNT FROM CM-CASH-CURR-MON-BAL      
            END-IF                                               
      WHEN  CREDIT-BALANCE-REFUND                                
            ADD CMT-AMNT TO CM-CASH-PRIN-STMT                    
      WHEN  INTR-EARNED-DB-ADJ                                   
            ADD CMT-AMNT TO CM-CASH-PRIN-STMT                    
      WHEN  INTR-EARNED-CR-ADJ                                   
            SUBTRACT CMT-AMNT FROM CM-CASH-PRIN-STMT             
      WHEN  OTHER                                                
            CONTINUE                                             
    END-EVALUATE.                                                
                                                                 
CCBP-EXIT.                                                       
    EXIT.                                                        
                                                                 
COMMON-GENERAL-BAL-POST.                                         
                                                                 
    EVALUATE TRUE                                                
                                                                 
      WHEN  RETAIL-INTR-DB-ADJUST                                
            ADD CMT-AMNT TO CM-RTL-BALANCE                       
      WHEN  RETAIL-SVC-CHRG-DB-ADJUST                            
            ADD CMT-AMNT TO CM-RTL-BALANCE                       
      WHEN  RETAIL-PURCHASE                                      
            ADD CMT-AMNT TO CM-RTL-BALANCE                       
      WHEN  RETAIL-DEBIT-ADJUST                                  
            ADD CMT-AMNT TO CM-RTL-BALANCE                       
      WHEN  MISC-FEE-DEBIT                                       
            ADD CMT-AMNT TO CM-RTL-BALANCE                       
      WHEN  INSURANCE-PREMIUM                                    
            ADD CMT-AMNT TO CM-RTL-BALANCE                       
      WHEN  RETAIL-INTR-CR-ADJUST                                
            IF  WS-PRE-STMT-FLAG = 'Y'                           
                MOVE 1 TO WSA-RTL-CREDIT-SW                      
                ADD CMT-AMNT TO CM-RTL-CR-ADJUST-BNP             
                PERFORM PROCESS-BAL-ADJUST THRU PBA-XIT          
            END-IF                                               
            SUBTRACT CMT-AMNT FROM CM-RTL-BALANCE                
      WHEN  RETAIL-CREDIT-ADJUST                                 
            IF  WS-PRE-STMT-FLAG = 'Y'                           
                MOVE 1 TO WSA-RTL-CREDIT-SW                      
                ADD CMT-AMNT TO CM-RTL-CR-ADJUST-BNP             
                PERFORM PROCESS-BAL-ADJUST THRU PBA-XIT          
            END-IF                                               
            SUBTRACT CMT-AMNT FROM CM-RTL-BALANCE                
      WHEN  MISC-FEE-CREDIT                                      
            IF  WS-PRE-STMT-FLAG = 'Y'                           
                MOVE 1 TO WSA-RTL-CREDIT-SW                      
                ADD CMT-AMNT TO CM-RTL-CR-ADJUST-BNP             
                PERFORM PROCESS-BAL-ADJUST THRU PBA-XIT          
            END-IF                                               
            SUBTRACT CMT-AMNT FROM CM-RTL-BALANCE                
      WHEN  RETAIL-SVC-CHRG-CR-ADJUST                            
            SUBTRACT CMT-AMNT FROM CM-RTL-BALANCE                
      WHEN  RETURNED-ITEM                                        
            SUBTRACT CMT-AMNT FROM CM-RTL-BALANCE                
      WHEN  INSURANCE-PREMIUM-REFUND                             
            SUBTRACT CMT-AMNT FROM CM-RTL-BALANCE                
      WHEN  CASH-INTR-DB-ADJUST                                  
            ADD CMT-AMNT TO CM-CASH-BALANCE                      
      WHEN  CASH-SVC-CHRG-DB-ADJUST                              
            ADD CMT-AMNT TO CM-CASH-BALANCE                      
      WHEN  CASH-ADVANCE                                         
            ADD CMT-AMNT TO CM-CASH-BALANCE                      
      WHEN  CASH-DEBIT-ADJUST                                    
            ADD CMT-AMNT TO CM-CASH-BALANCE                      
      WHEN  CASH-ADV-PER-FEE                                     
            ADD CMT-AMNT TO CM-CASH-BALANCE                      
      WHEN  CASH-INTR-CR-ADJUST                                  
            IF  WS-PRE-STMT-FLAG = 'Y'                           
                MOVE 1 TO WSA-CASH-CREDIT-SW                     
                ADD CMT-AMNT TO CM-CASH-CR-ADJUST-BNP            
                PERFORM PROCESS-BAL-ADJUST THRU PBA-XIT          
            END-IF                                               
            SUBTRACT CMT-AMNT FROM CM-CASH-BALANCE               
      WHEN  CASH-SVC-CHRG-CR-ADJUST                              
            IF  WS-PRE-STMT-FLAG = 'Y'                           
                MOVE 1 TO WSA-CASH-CREDIT-SW                     
                ADD CMT-AMNT TO CM-CASH-CR-ADJUST-BNP            
                PERFORM PROCESS-BAL-ADJUST THRU PBA-XIT          
            END-IF                                               
            SUBTRACT CMT-AMNT FROM CM-CASH-BALANCE               
      WHEN  CASH-ADVANCE-REVERSAL                                
            IF  WS-PRE-STMT-FLAG = 'Y'                           
                MOVE 1 TO WSA-CASH-CREDIT-SW                     
                ADD CMT-AMNT TO CM-CASH-CR-ADJUST-BNP            
                PERFORM PROCESS-BAL-ADJUST THRU PBA-XIT          
            END-IF                                               
            SUBTRACT CMT-AMNT FROM CM-CASH-BALANCE               
      WHEN  CASH-CREDIT-ADJUST                                   
            IF  WS-PRE-STMT-FLAG = 'Y'                           
                MOVE 1 TO WSA-CASH-CREDIT-SW                     
                ADD CMT-AMNT TO CM-CASH-CR-ADJUST-BNP            
                PERFORM PROCESS-BAL-ADJUST THRU PBA-XIT          
            END-IF                                               
            SUBTRACT CMT-AMNT FROM CM-CASH-BALANCE               
      WHEN  CASH-ADV-PER-FEE-REVERSAL                            
            IF  WS-PRE-STMT-FLAG = 'Y'                           
                MOVE 1 TO WSA-CASH-CREDIT-SW                     
                ADD CMT-AMNT TO CM-CASH-CR-ADJUST-BNP            
                PERFORM PROCESS-BAL-ADJUST THRU PBA-XIT          
            END-IF                                               
            SUBTRACT CMT-AMNT FROM CM-CASH-BALANCE               
      WHEN  CREDIT-BALANCE-REFUND                                
            ADD CMT-AMNT TO CM-CASH-BALANCE                      
            ADD CMT-AMNT TO CM-CURR-BALANCE                      
      WHEN  INTR-EARNED-DB-ADJ                                   
            ADD CMT-AMNT TO CM-CASH-BALANCE                      
      WHEN  INTR-EARNED-CR-ADJ                                   
            SUBTRACT CMT-AMNT FROM CM-CASH-BALANCE               
      WHEN  OTHER                                                
            CONTINUE                                             
    END-EVALUATE.                                                
                                                                 
CGBP-EXIT.                                                       
    EXIT.                                                        
                                                                 
GPS110GL-CR-ADJUST-DATE.                                         
    MOVE ZERO                  TO WS-STORE-TC.                   
    MOVE 'N'                   TO WS-PRE-STMT-FLAG.              
    IF CASH-ADVANCE-REVERSAL   OR                                
       MISC-FEE-CREDIT         OR                                
       CASH-SVC-CHRG-CR-ADJUST OR                                
       CASH-INTR-CR-ADJUST     OR                                
       RETAIL-INTR-CR-ADJUST   OR                                
       RETAIL-CREDIT-ADJUST    OR                                
       CASH-CREDIT-ADJUST                                        
       IF  CMT-ORIG-TRANS-DATE IS NOT NUMERIC                    
           MOVE ZEROES                    TO CMT-ORIG-TRANS-DATE 
       END-IF                                                    
       IF  CMT-ORIG-TRANS-DATE = ZERO                            
           MOVE 'Y'                       TO WS-PRE-STMT-FLAG    
       ELSE                                                      
           MOVE CMT-ORIG-TRANS-DATE       TO WS-DTE-JULIAN       
           PERFORM CCSI-JUL-TO-GREG     THRU CCSI-JTG-EXIT       
           MOVE WS-DTE-DATE               TO WS-DTE-DATEZ        
           PERFORM CCSI-DATE-VALIDATION THRU CCSI-DV-EXIT        
           IF  WS-DTE-DATE-IS-INVALID                            
               MOVE 'Y'                   TO WS-PRE-STMT-FLAG    
           ELSE                                                  
               IF CMT-ORIG-TRANS-DATE > CM-DTE-LST-STMT          
                  MOVE 'N'                TO WS-PRE-STMT-FLAG    
               ELSE                                              
                  MOVE 'Y'                TO WS-PRE-STMT-FLAG.   
GPS110GL-CAD-EXIT.                                               
    EXIT.                                                        
