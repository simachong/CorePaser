CCSI-FUTURE-DATE.
    IF  WS-DATE-FORMAT IS NOT NUMERIC                            
        MOVE ZERO               TO WS-DATE-FORMAT.               
    IF  WS-DTE-PERIOD = 'D'
        COMPUTE WS-DTE-DATE-1 = WS-DTE-JULDAYS + WS-DTE-FREQ
        MOVE ZERO               TO WS-DTE-RESULT
        PERFORM CCSI-FD-LEAP  THRU  CCSI-FD-LEAP-EXIT
        MOVE WS-DTE-DATE-1      TO WS-DTE-JULDAYS
        PERFORM CCSI-JUL-TO-GREG THRU CCSI-JTG-EXIT
        GO TO CCSI-FD-RESET-FIELDS.
    PERFORM CCSI-JUL-TO-GREG THRU CCSI-JTG-EXIT.                 
    IF  WS-DATE-FORMAT IS EQUAL TO 1                             
        MOVE WS-DTE-MON         TO WS-DATE-FORMAT-SAVE           
        MOVE WS-DTE-DAY         TO WS-DTE-MON                    
        MOVE WS-DATE-FORMAT-SAVE TO WS-DTE-DAY.                  
    IF  WS-DTE-PERIOD = 'Y'
        ADD WS-DTE-FREQ         TO WS-DTE-YEAR
        GO TO CCSI-FD-MONTH-YEAR-CONV.
    COMPUTE WS-DTE-DATE-1 = WS-DTE-MON + WS-DTE-FREQ.
    PERFORM CCSI-FD-MON-1  THRU  CCSI-FD-M1-EXIT
            UNTIL WS-DTE-DATE-1 IS LESS THAN 13.
    MOVE WS-DTE-DATE-1          TO WS-DTE-MON.
CCSI-FD-MONTH-YEAR-CONV.
    PERFORM CCSI-FD-SPECIFIC-DAY THRU CCSI-FD-SD-EXIT.
    IF  WS-DATE-FORMAT IS EQUAL TO 1                             
        MOVE WS-DTE-MON         TO WS-DATE-FORMAT-SAVE           
        MOVE WS-DTE-DAY         TO WS-DTE-MON                    
        MOVE WS-DATE-FORMAT-SAVE TO WS-DTE-DAY.                  
    PERFORM CCSI-GREG-TO-JUL THRU CCSI-GTJ-EXIT.
    PERFORM CCSI-JUL-TO-GREG THRU CCSI-JTG-EXIT.
CCSI-FD-RESET-FIELDS.
    MOVE ZERO                   TO WS-DTE-FREQ.
    MOVE 'D'                    TO WS-DTE-PERIOD.
    MOVE ZERO                   TO WS-DTE-DAY-OF-MON.
    GO TO CCSI-FD-EXIT.
CCSI-FD-LEAP.
    SUBTRACT WS-DTE-RESULT      FROM WS-DTE-DATE-1.              
    COMPUTE WS-DTE-WORK         =  WS-DTE-JULYR * 250.           
    IF  WS-DTE-RMDR IS GREATER THAN ZERO OR                      
        WS-DTE-SPECIAL-LEAP-YEAR-SKIP                            
        MOVE 365                TO WS-DTE-RESULT                 
    ELSE
        MOVE 366                TO WS-DTE-RESULT.                
    IF  WS-DTE-RESULT IS LESS THAN WS-DTE-DATE-1
        ADD 1                   TO WS-DTE-JULYR
        GO TO CCSI-FD-LEAP.
CCSI-FD-LEAP-EXIT.
    EXIT.
CCSI-FD-SPECIFIC-DAY.
    COMPUTE WS-DTE-WORK         = WS-DTE-JULYR * 250.            
    IF  WS-DTE-RMDR IS GREATER THAN ZERO OR                      
        WS-DTE-SPECIAL-LEAP-YEAR-SKIP                            
        MOVE 28                 TO WS-DV-MON (2)                 
    ELSE                                                         
        MOVE 29                 TO WS-DV-MON (2).                
    MOVE WS-DV-MON (WS-DTE-MM)  TO WS-DTE-DATE-1.                
    COMPUTE WS-DTE-WORK         =  WS-DTE-YEAR * 250.            
    IF  WS-DTE-RMDR IS GREATER THAN ZERO OR                      
        WS-DTE-SPECIAL-LEAP-YEAR-SKIP                            
        MOVE 28                 TO WS-DV-MON (2)                 
    ELSE
        MOVE 29                 TO WS-DV-MON (2).                
    IF  WS-DTE-DAY-OF-MON IS GREATER THAN ZERO AND
        WS-DTE-DAY-OF-MON IS LESS THAN 32
        IF  WS-DTE-DAY-OF-MON IS GREATER THAN
                            WS-DV-MON (WS-DTE-MON)
            MOVE WS-DV-MON (WS-DTE-MON) TO WS-DTE-DAY
            GO TO CCSI-FD-SD-EXIT
        ELSE
            MOVE WS-DTE-DAY-OF-MON TO WS-DTE-DAY
            GO TO CCSI-FD-SD-EXIT.
    IF  WS-DTE-DAY-OF-MON IS EQUAL TO 99
        IF  WS-DTE-DAY IS NOT LESS THAN WS-DTE-DATE-1            
            MOVE WS-DV-MON (WS-DTE-MON) TO WS-DTE-DAY
            GO TO CCSI-FD-SD-EXIT.
    IF  WS-DTE-DAY IS GREATER THAN WS-DV-MON (WS-DTE-MON)
        MOVE WS-DV-MON (WS-DTE-MON) TO WS-DTE-DAY.
CCSI-FD-SD-EXIT.
    EXIT.
CCSI-FD-MON-1.
    IF  WS-DTE-DATE-1 IS GREATER THAN 12
        ADD 1                   TO WS-DTE-YEAR
        SUBTRACT 12             FROM WS-DTE-DATE-1.
CCSI-FD-M1-EXIT.
    EXIT.
CCSI-FD-EXIT.
    EXIT.
