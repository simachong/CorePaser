ABPS-MAIN-FUNCTION SECTION.                                      
    PERFORM  ABPS-MAIN-DATA-CHECK THRU ABPS-MDC-XIT.             
                                                                 
    EVALUATE TRUE                                                
       WHEN CPS422-INITIALIZE-FUNC                               
            PERFORM ABPS-PROGRAM-INITIALIZE  THRU ABPS-PI-XIT    
       WHEN CPS422-SHUTDOWN-FUNC                                 
            PERFORM ABPS-PROGRAM-SHUTDOWN    THRU ABPS-PGM-SD-XIT
       WHEN CPS422-ORG-LEVEL-FUNC                                
            PERFORM ABPS-PROCESS-ORGS        THRU ABPS-PO-XIT    
       WHEN CPS422-TYPE-LEVEL-FUNC                               
            PERFORM ABPS-PROCESS-TYPE        THRU ABPS-P-TYPE-XIT
       WHEN CPS422-ACCT-LEVEL-FUNC                               
            PERFORM ABPS-PROCESS-ACCOUNTS    THRU ABPS-PA-XIT    
       WHEN CPS422-ACCT-INTR-FUNC                                
            PERFORM ABPS-PROCESS-ACCT-INTR   THRU ABPS-PAI-XIT   
       WHEN CPS422-ACCT-TRAN-FUNC                                
            PERFORM ABPS-PROCESS-ACCT-TRAN   THRU ABPS-PAT-XIT   
       WHEN CPS422-PYMT-NETOUT-FUNC                              
            PERFORM ABPS-PROCESS-PYMT-NETOUT THRU ABPS-PPN-XIT   
       WHEN CPS422-LEDGER-FUNC                                   
            PERFORM ABPS-PROCESS-LEDGER      THRU ABPS-PL-XIT    
       WHEN OTHER                                                
            CONTINUE                                             
    END-EVALUATE.                                                
                                                                 
ABPS-MF-XIT.                                                     
    EXIT.                                                        
                                                                 
ABPS-MAIN-DATA-CHECK.                                            
    IF  NOT CPS422-VALID-FUNC                                    
        MOVE 60001           TO WS-ABEND-CODE                    
        MOVE 'CPBABP FUNCTION CODE ERROR'                        
                             TO WS-ABENDMSG                      
        MOVE '00'            TO WS-ABEND-STATUS                  
        PERFORM CCSI-ABEND THRU CCSI-ABEND-EXIT                  
    END-IF.                                                      
                                                                 
ABPS-MDC-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-PROGRAM-INITIALIZE.                                         
                                                                 
    INITIALIZE   ABP-RECORD-INIT                                 
                 WS-BPC-CONTROL-RECORD-INIT                      
                 WSBP-BPE-CONTROL-RECORD.                        
                                                                 
    MOVE ABP-RECORD-INIT            TO WSBP-ABP-INPUT-RECORD     
                                       WSBP-ABP-CONTROL-RECORD.  
    MOVE WS-BPC-CONTROL-RECORD-INIT TO WS-BPC-CONTROL-RECORD.    
                                                                 
    MOVE ZERO    TO ABP1-NO-OF-ENTRY                             
                    ABP2-NO-OF-ENTRY.                            
                                                                 
    MOVE ZERO                       TO CPS404-FILE-NMBR.         
    MOVE 1                          TO CPS404-SEGMENTATION-SW.   
    MOVE 60002                      TO WS-ABEND-CODE.            
    PERFORM CIA-ABP-OPEN-INPUT    THRU CIA-ABP-OI-EXIT.          
    IF  NOT WS-IO-COMPLETE                                       
        MOVE 'INVALID CPBABP OPEN'  TO WS-ABENDMSG               
        MOVE CPS404-FILE-STATUS     TO WS-ABEND-STATUS           
        PERFORM CCSI-ABEND        THRU CCSI-ABEND-EXIT           
    END-IF.                                                      
                                                                 
                                                                 
    MOVE ZEROES                     TO WS-ABP-ORG-NMBR.          
    MOVE ZEROES                     TO WS-ABP-TYP-NMBR.          
    MOVE ZEROES                     TO WS-ABP-ACCT-NMBR.         
    MOVE ZEROES                     TO WS-ABP-BAL-SEQ-NMBR.      
    MOVE 60003                      TO WS-ABEND-CODE.            
    PERFORM CIA-ABP-READ-RANDOM   THRU CIA-ABP-RR-EXIT.          
    IF  NOT WS-IO-COMPLETE                                       
        MOVE 'INVALID CPBABP HEADER READ' TO WS-ABENDMSG         
        MOVE CPS404-FILE-STATUS           TO WS-ABEND-STATUS     
        PERFORM CCSI-ABEND              THRU CCSI-ABEND-EXIT     
    END-IF.                                                      
                                                                 
    IF ABP-ORG-NMBR EQUAL TO ZERO                                
       MOVE WSBP-ABP-CONTROL-RECORD TO WSBP-ABP-HEADER-RECORD    
       IF  ABP-FILE-PROC-DTE   IS EQUAL TO ZERO OR               
          (ABP-FILE-PROC-DTE   IS EQUAL TO SC-DTE-LST-PROC  AND  
           ABP-FILE-TIME-STAMP IS EQUAL TO SC-LST-TIME-STAMP)    
           CONTINUE                                              
       ELSE                                                      
           MOVE 60004               TO WS-ABEND-CODE             
           MOVE 'INVALID HEADER IN INPUT CPBABP' TO WS-ABENDMSG  
           PERFORM CCSI-ABEND     THRU CCSI-ABEND-EXIT           
       END-IF                                                    
    ELSE                                                         
       MOVE 60005                   TO WS-ABEND-CODE             
       MOVE 'NO HEADER IN INPUT CPBABP'  TO WS-ABENDMSG          
       PERFORM CCSI-ABEND         THRU CCSI-ABEND-EXIT           
    END-IF.                                                      
                                                                 
    MOVE 1                          TO CPS404-FILE-NMBR.         
    MOVE 1                          TO CPS404-SEGMENTATION-SW.   
    MOVE 60006                      TO WS-ABEND-CODE.            
    PERFORM CIA-ABP-OPEN-OUTPUT   THRU CIA-ABP-OO-EXIT.          
    IF  NOT WS-IO-COMPLETE                                       
        MOVE 'INVALID CPBABPX OPEN' TO WS-ABENDMSG               
        MOVE CPS404-FILE-STATUS     TO WS-ABEND-STATUS           
        PERFORM CCSI-ABEND        THRU CCSI-ABEND-EXIT           
    END-IF.                                                      
                                                                 
    MOVE SC-TODAYS-JULIAN           TO ABP-FILE-PROC-DTE.        
    MOVE SC-TIME-STAMP              TO ABP-FILE-TIME-STAMP.      
    MOVE WSBP-ABP-HEADER-RECORD     TO WSBP-ABP-CONTROL-RECORD.  
    PERFORM ABPS-CIA-WRITE-ABP-FILE THRU ABPS-CWAF-XIT.          
                                                                 
    SET CPS420-OPEN-FILE            TO TRUE.                     
    SET CPS420-SEARCH-ACTIVE        TO TRUE.                     
    MOVE ZERO                       TO CPS420-RETURN-CODE.       
    CALL 'CPBPRO'                USING CPS420-REQUEST-BLOCK      
                                       WS-BPC-CONTROL-RECORD.    
    IF  NOT CPS420-REC-FOUND                                     
        MOVE 'OPEN ERROR ON CPBPC FILE' TO WS-ABENDMSG           
        MOVE 60007                      TO WS-ABEND-CODE         
        MOVE '00'                       TO WS-ABEND-STATUS       
        PERFORM CCSI-ABEND            THRU CCSI-ABEND-EXIT       
    END-IF.                                                      
                                                                 
                                                                 
    PERFORM ABPS-CIA-READ-ABP-FILE    THRU ABPS-CRAF-XIT.        
                                                                 
ABPS-PI-XIT.                                                     
    EXIT.                                                        
                                                                 
ABPS-PROGRAM-SHUTDOWN.                                           
                                                                 
    MOVE ZERO                       TO CPS404-FILE-NMBR.         
    MOVE 60008                      TO WS-ABEND-CODE.            
    PERFORM CIA-ABP-CLOSE         THRU CIA-ABP-C-EXIT.           
    IF  NOT WS-IO-COMPLETE                                       
        MOVE 'INVALID CPBABP CLOSE' TO WS-ABENDMSG               
        MOVE CPS404-FILE-STATUS     TO WS-ABEND-STATUS           
        PERFORM CCSI-ABEND        THRU CCSI-ABEND-EXIT           
    END-IF.                                                      
                                                                 
                                                                 
    INITIALIZE WSBP-ABP-HEADER-RECORD.                           
    MOVE +999                       TO ABP-H-ORG-NMBR.           
    MOVE WSBP-ABP-HEADER-RECORD     TO WSBP-ABP-CONTROL-RECORD.  
    PERFORM ABPS-CIA-WRITE-ABP-FILE THRU ABPS-CWAF-XIT.          
                                                                 
                                                                 
    MOVE 1                           TO CPS404-FILE-NMBR.        
    MOVE 60009                       TO WS-ABEND-CODE.           
    PERFORM CIA-ABP-CLOSE          THRU CIA-ABP-C-EXIT.          
    IF  NOT WS-IO-COMPLETE                                       
        MOVE 'INVALID CPBABPX CLOSE' TO WS-ABENDMSG              
        MOVE CPS404-FILE-STATUS      TO WS-ABEND-STATUS          
        PERFORM CCSI-ABEND         THRU CCSI-ABEND-EXIT          
    END-IF.                                                      
                                                                 
     SET CPS420-CLOSE-FILE           TO TRUE.                    
     MOVE ZERO                       TO CPS420-RETURN-CODE.      
     CALL 'CPBPRO'                USING CPS420-REQUEST-BLOCK     
                                        WS-BPC-CONTROL-RECORD.   
                                                                 
    IF  WSBP-ABP-WARNING-TRUE                                    
        DISPLAY 'WARNING ENTRY HAS BEEN REACHED'                 
        MOVE   +1                    TO RETURN-CODE              
    END-IF.                                                      
                                                                 
ABPS-PGM-SD-XIT.                                                 
    EXIT.                                                        
                                                                 
ABPS-PROCESS-ORGS.                                               
    EVALUATE CPS422-FUNCTION-CODE                                
       WHEN +1010                                                
           PERFORM ABPS-INITIALIZE-ORG THRU ABPS-IO-XIT          
       WHEN OTHER                                                
           CONTINUE                                              
    END-EVALUATE.                                                
                                                                 
ABPS-PO-XIT.                                                     
    EXIT.                                                        
                                                                 
ABPS-PROCESS-TYPE.                                               
                                                                 
    EVALUATE CPS422-FUNCTION-CODE                                
       WHEN +2010                                                
           PERFORM ABPS-INITIALIZE-TYPE THRU ABPS-IT-XIT         
       WHEN OTHER                                                
           CONTINUE                                              
    END-EVALUATE.                                                
                                                                 
ABPS-P-TYPE-XIT.                                                 
    EXIT.                                                        
                                                                 
ABPS-PROCESS-ACCOUNTS.                                           
    EVALUATE CPS422-FUNCTION-CODE                                
       WHEN +3010                                                
           PERFORM ABPS-ACCT-LOAD-BP THRU ABPS-ALB-XIT           
       WHEN +3040                                                
           PERFORM ABPS-AUTO-CHGOFF-REVERSAL THRU ABPS-ACR-EXIT  
       WHEN +3050                                                
           PERFORM ABPS-MIGRATE-CHGOFF-STATUS THRU ABPS-MCS-XIT  
       WHEN +3120                                                
           PERFORM ABPS-RESET-BAL-PROG-FIELDS THRU ABPS-RBPF-XIT 
       WHEN +3130                                                
           PERFORM ABPS-RESET-BAL-PROG-FIELDS THRU ABPS-RBPF-XIT 
       WHEN +3160                                                
           PERFORM ABPS-PROCESS-CHARGE-OFF THRU ABPS-PCO-XIT     
       WHEN +3170                                                
           PERFORM ABPS-PROCESS-CO-REVERSAL THRU ABPS-PCOR-XIT   
       WHEN +3190                                                
           PERFORM ABPS-RESET-BAL-PROG-FIELDS THRU ABPS-RBPF-XIT 
       WHEN +3230                                                
           PERFORM ABPS-SET-ENDING-CO-DISPLAY THRU ABPS-SECO-XIT 
       WHEN +3260                                                
           PERFORM ABPS-COMPUTE-CURR-DUE THRU ABPS-CCD-XIT       
       WHEN +3280                                                
           PERFORM ABPS-BALANCE-BUCKET-CYCLING THRU ABPS-BBC-XIT 
       WHEN +3610                                                
           PERFORM ABPS-ACCUMULATE-PROC THRU ABPS-ACP-XIT        
       WHEN +3999                                                
           PERFORM ABPS-UPDATE-BAL-PROGRAM THRU ABPS-UBP-XIT     
       WHEN OTHER                                                
           CONTINUE                                              
    END-EVALUATE.                                                
                                                                 
ABPS-PA-XIT.                                                     
    EXIT.                                                        
                                                                 
ABPS-PROCESS-ACCT-INTR.                                          
                                                                 
    EVALUATE CPS422-FUNCTION-CODE                                
       WHEN +4330                                                
         PERFORM ABPS-COMPUTE-RTL-BEARING-BAL THRU ABPS-CRBB-XIT 
       WHEN +4340                                                
         PERFORM ABPS-COMPUTE-CSH-BEARING-BAL THRU ABPS-CCBB-XIT 
       WHEN +4350                                                
         PERFORM ABPS-COMPUTE-PROVIS-BAL THRU ABPS-CPB-XIT       
       WHEN OTHER                                                
         CONTINUE                                                
    END-EVALUATE.                                                
                                                                 
ABPS-PAI-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-PROCESS-ACCT-TRAN.                                          
    EVALUATE CPS422-FUNCTION-CODE                                
       WHEN +5010                                                
          PERFORM ABPS-TRANS-BAL-PROG-CHECK THRU ABPS-TBPC-XIT   
       WHEN +5020                                                
          PERFORM ABPS-PROCESS-SALES THRU ABPS-PSLS-XIT          
       WHEN +5030                                                
          PERFORM ABPS-PROCESS-RETURN THRU ABPS-RTN-XIT          
       WHEN +5060                                                
          PERFORM ABPS-PROCESS-RTL-ADJUST  THRU ABPS-PRA-XIT     
       WHEN +5090                                                
          PERFORM ABPS-PROCESS-ABP-FEES THRU ABPS-PAF-XIT        
       WHEN +5110                                                
          PERFORM ABPS-PROCESS-DISPUTED-ITEMS THRU ABPS-PDI-XIT  
       WHEN +5120                                                
          PERFORM ABPS-PROCESS-INTR-ADJUST THRU ABPS-PIA-XIT     
       WHEN OTHER                                                
          CONTINUE                                               
    END-EVALUATE.                                                
                                                                 
ABPS-PAT-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-PROCESS-PYMT-NETOUT.                                        
                                                                 
    EVALUATE CPS422-FUNCTION-CODE                                
       WHEN +6010                                                
          PERFORM ABPS-PROCESS-PAID-IN-FULL THRU ABPS-PPIF-XIT   
       WHEN +6020                                                
          PERFORM ABPS-PROCESS-NOT-PAID-FULL THRU ABPS-PNPF-XIT  
       WHEN +6030                                                
          PERFORM ABPS-BALANCING-ACCT-ROUTINE THRU ABPS-BAR-XIT  
       WHEN +6040                                                
          PERFORM ABPS-BALANCING-ACCT-ROUTINE THRU ABPS-BAR-XIT  
       WHEN OTHER                                                
          CONTINUE                                               
    END-EVALUATE.                                                
                                                                 
ABPS-PPN-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-PROCESS-LEDGER.                                             
    EVALUATE CPS422-FUNCTION-CODE                                
       WHEN +8010                                                
           PERFORM ABPS-WRITE-CPCDL          THRU ABPS-WC-XIT    
       WHEN +8020                                                
           PERFORM ABPS-SAVE-ABP-BALANCE     THRU ABPS-SAB-XIT   
       WHEN +8030                                                
           PERFORM ABPS-ABP-BAL-CHANGE       THRU ABPS-ABC-XIT   
       WHEN +8040                                                
           PERFORM ABPS-CHARGE-OFF-GL        THRU ABPS-COG-XIT   
       WHEN OTHER                                                
           CONTINUE                                              
    END-EVALUATE.                                                
ABPS-PL-XIT.                                                     
    EXIT.                                                        
                                                                 
ABPS-INITIALIZE-ORG.                                             
    MOVE ZERO    TO BPE1-NO-OF-ENTRY.                            
    PERFORM ABPS-IO-LOAD-BPE1 THRU ABPS-IO-LB1-XIT.              
                                                                 
ABPS-IO-XIT.                                                     
    EXIT.                                                        
                                                                 
ABPS-IO-LOAD-BPE1.                                               
    SET CPS420-CACHE-GET-FIRST         TO TRUE.                  
    SET CPS420-SEARCH-ACTIVE           TO TRUE.                  
    SET CPS420-REC-NOT-FOUND           TO TRUE.                  
                                                                 
    MOVE WS-BPC-CONTROL-RECORD-INIT    TO WS-BPC-CONTROL-RECORD. 
    MOVE OC-ORGN-NMBR                  TO BPC-ORG-NMBR.          
    PERFORM ABPS-CALL-CPBPRO-ROUTINE THRU ABPS-CPBPRO-XIT.       
    PERFORM UNTIL NOT CPS420-REC-FOUND                           
      ADD  1                TO BPE1-NO-OF-ENTRY                  
      SET  BPE1-IDX         TO BPE1-NO-OF-ENTRY                  
      MOVE BPC-ORG-NMBR     TO BPE1-ORG-NMBR (BPE1-IDX)          
      MOVE BPC-BAL-PGM-NMBR TO BPE1-BAL-PGM-NMBR (BPE1-IDX)      
      MOVE BPC-BAL-TYPE     TO BPE1-BAL-TYPE (BPE1-IDX)          
      MOVE BPC-BAL-PROGRAM-TYPE                                  
                            TO BPE1-BAL-PROGRAM-TYPE (BPE1-IDX)  
      MOVE BPC-BAL-APP-IND  TO BPE1-BAL-APP-IND (BPE1-IDX)       
      MOVE BPC-DESCRIPTION  TO BPE1-DESCRIPTION (BPE1-IDX)       
      MOVE BPC-DATE-START   TO BPE1-DATE-START (BPE1-IDX)        
      MOVE BPC-DATE-END     TO BPE1-DATE-END (BPE1-IDX)          
                                                                 
      SET CPS420-CACHE-GET-NEXT          TO TRUE                 
      PERFORM ABPS-CALL-CPBPRO-ROUTINE THRU ABPS-CPBPRO-XIT      
    END-PERFORM.                                                 
                                                                 
ABPS-IO-LB1-XIT.                                                 
    EXIT.                                                        
                                                                 
ABPS-CALL-CPBPRO-ROUTINE.                                        
     CALL 'CPBPRO'              USING CPS420-REQUEST-BLOCK       
                                      WS-BPC-CONTROL-RECORD.     
     IF CPS420-REC-FOUND OR CPS420-REC-NOT-FOUND                 
        CONTINUE                                                 
     ELSE                                                        
        MOVE 60010                 TO WS-ABEND-CODE              
        MOVE 'INVALID CPBPRO CALL' TO WS-ABENDMSG                
        MOVE CPS420-RETURN-CODE    TO WS-ABEND-STATUS            
        PERFORM CCSI-ABEND       THRU CCSI-ABEND-EXIT            
     END-IF.                                                     
                                                                 
ABPS-CPBPRO-XIT.                                                 
    EXIT.                                                        
                                                                 
ABPS-INITIALIZE-TYPE.                                            
    MOVE ZERO    TO BPE2-NO-OF-ENTRY.                            
    MOVE SPACE   TO WSBP-BAL-LIMT-CUST-NBR-RTL.                  
    PERFORM ABPS-IO-LOAD-BPE2 THRU ABPS-IO-LB2-XIT.              
                                                                 
ABPS-IT-XIT.                                                     
    EXIT.                                                        
                                                                 
ABPS-IO-LOAD-BPE2.                                               
    PERFORM VARYING BPE2-IDX FROM 1 BY 1                         
            UNTIL   BPE2-IDX IS GREATER THAN BPE2-TABLE-MAX      
      MOVE ZERO              TO BPE2-ORG-NMBR         (BPE2-IDX) 
      MOVE SPACE             TO BPE2-BAL-PGM-NMBR     (BPE2-IDX) 
                                BPE2-BAL-TYPE         (BPE2-IDX) 
                                BPE2-BAL-PROGRAM-TYPE (BPE2-IDX) 
                                BPE2-BAL-APP-IND      (BPE2-IDX) 
                                BPE2-DESCRIPTION      (BPE2-IDX) 
      MOVE ZERO              TO BPE2-DATE-START       (BPE2-IDX) 
                                BPE2-DATE-END         (BPE2-IDX) 
      MOVE SPACE             TO BPE2-USER-FILLER      (BPE2-IDX) 
                         BPE2-PLAN-LIMIT-CUST-ABP-NMBR(BPE2-IDX) 
                                BPE2-FILLER           (BPE2-IDX) 
    END-PERFORM.                                                 
                                                                 
    IF  WSGCP-FOUND = 'Y'                                        
        MOVE GCPPARM-BAL-LIMT-CUST-NBR-RTL                       
                             TO WSBP-BAL-LIMT-CUST-NBR-RTL       
        MOVE TC-ORGN-NMBR    TO WSBP-BPE1-INPUT-ORG-NMBR         
        PERFORM VARYING X-ABP-BAL-MAP FROM 1 BY 1                
          UNTIL   X-ABP-BAL-MAP IS GREATER THAN WS-BP-BPID-TOTAL 
          IF  GCPPARM-ABP-BAL-LIMT-BP-ID(X-ABP-BAL-MAP)          
              NOT EQUAL TO SPACE                                 
              MOVE GCPPARM-ABP-BAL-LIMT-BP-ID(X-ABP-BAL-MAP)     
                             TO WSBP-BPE1-INPUT-BAL-PROG         
              PERFORM ABPS-SEARCH-BPE1-TABLE THRU ABPS-SBPE1-XIT 
              IF  WSBP-BPE1-FOUND                                
                ADD  1                 TO BPE2-NO-OF-ENTRY       
                SET  BPE2-IDX          TO BPE2-NO-OF-ENTRY       
                MOVE GCPPARM-BAL-LIMT-CUST-NBR-ABP(X-ABP-BAL-MAP)
                     TO BPE2-PLAN-LIMIT-CUST-ABP-NMBR(BPE2-IDX)  
                MOVE BPE1-KEY(BPE1-IDX)                          
                     TO BPE2-KEY(BPE2-IDX)                       
                MOVE BPE1-DETAIL-DATA(BPE1-IDX)                  
                     TO BPE2-DETAIL-DATA(BPE2-IDX)               
              END-IF                                             
          END-IF                                                 
        END-PERFORM                                              
    END-IF.                                                      
                                                                 
ABPS-IO-LB2-XIT.                                                 
    EXIT.                                                        
                                                                 
ABPS-SEARCH-BPE1-TABLE.                                          
    SET BPE1-IDX     TO 1.                                       
    SEARCH BPE1-RECORD-TABLE                                     
      AT END                                                     
         SET WSBP-BPE1-NOT-FOUND TO TRUE                         
      WHEN BPE1-KEY (BPE1-IDX) EQUAL TO WSBP-BPE1-INPUT-KEY      
         SET WSBP-BPE1-FOUND     TO TRUE                         
    END-SEARCH.                                                  
                                                                 
ABPS-SBPE1-XIT.                                                  
    EXIT.                                                        
                                                                 
                                                                 
ABPS-ACCT-LOAD-BP.                                               
    PERFORM VARYING ABP2-IDX  FROM 1 BY 1                        
            UNTIL   ABP2-IDX  IS GREATER THAN WS-BP-BPID-TOTAL   
       MOVE SPACE             TO ABP2-BPID(ABP2-IDX)             
                                 ABP2-LIMT-NBR(ABP2-IDX)         
       MOVE ZERO              TO ABP2-ABP-BALANCE(ABP2-IDX)      
       MOVE ZERO              TO ABP2-CURR-MON-BAL(ABP2-IDX)     
       MOVE ZERO              TO ABP2-PRIN-STMT(ABP2-IDX)        
    END-PERFORM.                                                 
                                                                 
    IF WSBP-ABP-EOF-TRU OR WSBP-ABP-TRAILER                      
       GO TO ABPS-ALB-XIT                                        
    END-IF.                                                      
                                                                 
    PERFORM UNTIL WSBP-ABP-ACT-KEY >= CM-KEY   OR                
                  WSBP-ABP-EOF-TRU       OR                      
                  WSBP-ABP-TRAILER                               
       MOVE WSBP-ABP-INPUT-RECORD   TO WSBP-ABP-CONTROL-RECORD   
       PERFORM ABPS-CIA-WRITE-ABP-FILE THRU ABPS-CWAF-XIT        
       PERFORM ABPS-CIA-READ-ABP-FILE  THRU ABPS-CRAF-XIT        
    END-PERFORM.                                                 
                                                                 
    IF WSBP-ABP-EOF-TRU OR                                       
       WSBP-ABP-TRAILER OR                                       
       WSBP-ABP-ACT-KEY > CM-KEY                                 
       GO TO ABPS-ALB-XIT                                        
    END-IF.                                                      
                                                                 
    PERFORM UNTIL WSBP-ABP-ACT-KEY > CM-KEY   OR                 
                  WSBP-ABP-EOF-TRU            OR                 
                  WSBP-ABP-TRAILER                               
       MOVE WSBP-ABP-INPUT-RECORD   TO WSBP-ABP-CONTROL-RECORD   
       SET WSBP-ABP1-LOAD-OPTION    TO TRUE                      
       PERFORM ABPS-OPERATION-ABP-PROC THRU ABPS-OAP-XIT         
       PERFORM ABPS-CIA-READ-ABP-FILE  THRU ABPS-CRAF-XIT        
    END-PERFORM.                                                 
                                                                 
ABPS-ALB-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-CIA-READ-ABP-FILE.                                          
    MOVE ZERO                       TO CPS404-FILE-NMBR.         
    MOVE 60011                      TO WS-ABEND-CODE.            
    PERFORM CIA-ABP-READ-SEQ      THRU CIA-ABP-RSEQ-EXIT.        
    IF  NOT WS-IO-COMPLETE AND                                   
        NOT WS-END-OF-FILE                                       
        MOVE 'INVALID CPBABP READ'  TO WS-ABENDMSG               
        MOVE CPS404-FILE-STATUS     TO WS-ABEND-STATUS           
        PERFORM CCSI-ABEND        THRU CCSI-ABEND-EXIT           
    ELSE                                                         
       IF WS-END-OF-FILE                                         
          SET WSBP-ABP-EOF-TRU      TO TRUE                      
          MOVE +999                 TO WSBP-ABP-ACT-ORG-NMBR     
          MOVE +999                 TO WSBP-ABP-ACT-TYPE         
          MOVE +9999999999999999    TO WSBP-ABP-ACT-ACT-NMBR     
       ELSE                                                      
          IF ABP-ORG-NMBR EQUAL TO 999                           
             SET WSBP-ABP-TRAILER   TO TRUE                      
             MOVE +999              TO WSBP-ABP-ACT-ORG-NMBR     
             MOVE +999              TO WSBP-ABP-ACT-TYPE         
             MOVE +9999999999999999 TO WSBP-ABP-ACT-ACT-NMBR     
          ELSE                                                   
             MOVE ABP-ORG-NMBR      TO WSBP-ABP-ACT-ORG-NMBR     
             MOVE ABP-TYP-NMBR      TO WSBP-ABP-ACT-TYPE         
             MOVE ABP-ACCOUNT-NMBR  TO WSBP-ABP-ACT-ACT-NMBR     
          END-IF                                                 
          MOVE WSBP-ABP-CONTROL-RECORD                           
                                    TO WSBP-ABP-INPUT-RECORD     
       END-IF                                                    
    END-IF.                                                      
                                                                 
ABPS-CRAF-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-CIA-WRITE-ABP-FILE.                                         
    MOVE 1                           TO CPS404-FILE-NMBR.        
    MOVE 60012                       TO WS-ABEND-CODE.           
    PERFORM CIA-ABP-RECORD-ADD     THRU CIA-ABP-ADD-EXIT.        
    IF  NOT WS-IO-COMPLETE                                       
        MOVE 'INVALID CPBABPX WRITE' TO WS-ABENDMSG              
        MOVE CPS404-FILE-STATUS      TO WS-ABEND-STATUS          
        PERFORM CCSI-ABEND         THRU CCSI-ABEND-EXIT          
    END-IF.                                                      
                                                                 
ABPS-CWAF-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-OPERATION-ABP-PROC.                                         
    EVALUATE TRUE                                                
      WHEN WSBP-ABP1-GET-OPTION                                  
           SET WSBP-ABP1-NOT-FOUND            TO TRUE            
           PERFORM ABPS-SEARCH-ABP-BY-TXN   THRU ABPS-SABPBT-XIT 
      WHEN WSBP-ABP1-ADD-OPTION                                  
           PERFORM ABPS-CREATE-ABP-RECORD   THRU ABPS-CAR-XIT    
      WHEN WSBP-ABP1-LOAD-OPTION                                 
       PERFORM ABPS-LOAD-ABP-TO-MEMORY      THRU ABPS-LATM-XIT   
    END-EVALUATE.                                                
                                                                 
    IF (WSBP-ABP1-GET-OPTION AND (NOT WSBP-ABP1-NOT-FOUND)) OR   
        WSBP-ABP1-ADD-OPTION      OR                             
        WSBP-ABP1-REACTIVE-OPTION OR                             
        WSBP-ABP1-LOAD-OPTION                                    
      MOVE ABP-ORG-NMBR          TO WSBP-ABP-TXN-ORG-NMBR        
      MOVE ABP-TYP-NMBR          TO WSBP-ABP-TXN-TYPE            
      MOVE ABP-ACCOUNT-NMBR      TO WSBP-ABP-TXN-ACT-NMBR        
      MOVE ABP-ACT-BAL-PROGRAM   TO WSBP-ABP-TXN-BAL-PROG        
      MOVE ABP-ACT-BAL-SEQ-NMBR  TO WSBP-ABP-TXN-BAL-SEQ-NMBR    
    END-IF.                                                      
                                                                 
ABPS-OAP-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-SEARCH-ABP-BY-TXN.                                          
    SET ABP1-IDX                     TO 1.                       
    SEARCH ABP1-RECORD-TABLE                                     
      AT END                                                     
           SET WSBP-ABP1-NOT-FOUND   TO TRUE                     
      WHEN ABP-MULTI-TXN-BAL-ONLY                          AND   
           WSBP-ABPTI-ORG-NMBR EQUAL TO                          
                               ABP1-ORG-NMBR(ABP1-IDX)     AND   
           WSBP-ABPTI-TYPE     EQUAL TO                          
                               ABP1-TYP-NMBR(ABP1-IDX)     AND   
           WSBP-ABPTI-ACT-NMBR EQUAL TO                          
                               ABP1-ACCOUNT-NMBR(ABP1-IDX) AND   
           WSBP-ABPTI-BAL-PROG EQUAL TO                          
                               ABP1-ACT-BAL-PROGRAM(ABP1-IDX)    
           SET WSBP-ABP1-FOUND       TO TRUE                     
           MOVE ABP1-RECORD-TBL (ABP1-IDX)                       
                                     TO WSBP-ABP-CONTROL-RECORD  
    END-SEARCH.                                                  
                                                                 
ABPS-SABPBT-XIT.                                                 
    EXIT.                                                        
                                                                 
ABPS-CREATE-ABP-RECORD.                                          
    MOVE CM-NEXT-BAL-SEQ-NMBR      TO ABP-ACT-BAL-SEQ-NMBR.      
    ADD 1                          TO CM-NEXT-BAL-SEQ-NMBR.      
    MOVE ABP-ORG-NMBR              TO WSBP-BPE2-INPUT-ORG-NMBR.  
    MOVE ABP-ACT-BAL-PROGRAM       TO WSBP-BPE2-INPUT-BAL-PROG.  
    PERFORM ABPS-SEARCH-BPE2-TABLE    THRU ABPS-SBPE2-XIT.       
    PERFORM ABPS-SEARCH-BPE2-ERROR    THRU ABPS-SBPE2E-XIT.      
    PERFORM ABPS-SET-ABP-PART1-RECORD THRU ABPS-SAP1R-XIT.       
    PERFORM ABPS-LOAD-ABP-TO-MEMORY   THRU ABPS-LATM-XIT.        
    MOVE ABP1-RECORD-TBL (ABP1-IDX)                              
                                   TO WSBP-ABP-CONTROL-RECORD.   
    PERFORM ABPS-SET-ABP-PART2-RECORD THRU ABPS-SAP2R-XIT.       
                                                                 
ABPS-CAR-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-LOAD-ABP-TO-MEMORY.                                         
    MOVE ABP-ORG-NMBR        TO WSBP-BPE2-INPUT-ORG-NMBR.        
    MOVE ABP-ACT-BAL-PROGRAM TO WSBP-BPE2-INPUT-BAL-PROG.        
    PERFORM ABPS-SEARCH-BPE2-TABLE THRU ABPS-SBPE2-XIT.          
    PERFORM ABPS-SEARCH-BPE2-ERROR THRU ABPS-SBPE2E-XIT.         
                                                                 
    ADD 1                    TO ABP1-NO-OF-ENTRY.                
    IF  ABP1-NO-OF-ENTRY > ABP1-TABLE-MAX                        
        MOVE 60013           TO WS-ABEND-CODE                    
        MOVE 'EXCEED THE MAX ENTRY ' TO WS-ABENDMSG              
        MOVE '00'            TO WS-ABEND-STATUS                  
        PERFORM CCSI-ABEND THRU CCSI-ABEND-EXIT                  
    ELSE                                                         
      IF  ABP1-NO-OF-ENTRY > ABP1-TABLE-MAX-WARN                 
          SET WSBP-ABP-WARNING-TRUE  TO TRUE                     
      END-IF                                                     
    END-IF.                                                      
                                                                 
    SET  ABP1-IDX            TO ABP1-NO-OF-ENTRY.                
    MOVE WSBP-ABP-CONTROL-RECORD                                 
                             TO ABP1-RECORD-TBL (ABP1-IDX).      
                                                                 
ABPS-LATM-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-SEARCH-BPE2-TABLE.                                          
    SET BPE2-IDX     TO 1.                                       
    SEARCH BPE2-RECORD-TABLE                                     
         AT END                                                  
            SET WSBP-BPE2-NOT-FOUND TO TRUE                      
         WHEN BPE2-KEY (BPE2-IDX) EQUAL TO WSBP-BPE2-INPUT-KEY   
            SET WSBP-BPE2-FOUND     TO TRUE                      
            MOVE BPE2-RECORD-TABLE (BPE2-IDX)                    
                                    TO WSBP-BPE-CONTROL-RECORD   
    END-SEARCH.                                                  
                                                                 
ABPS-SBPE2-XIT.                                                  
    EXIT.                                                        
                                                                 
ABPS-SEARCH-BPE2-ERROR.                                          
    IF  WSBP-BPE2-NOT-FOUND                                      
        MOVE 60014 TO WS-ABEND-CODE                              
        MOVE 'BAL PROG NOT DEFINED' TO WS-ABENDMSG               
        MOVE '00'                   TO WS-ABEND-STATUS           
        PERFORM CCSI-ABEND        THRU CCSI-ABEND-EXIT           
    END-IF.                                                      
                                                                 
ABPS-SBPE2E-XIT.                                                 
    EXIT.                                                        
                                                                 
ABPS-SET-ABP-PART1-RECORD.                                       
    MOVE BPE-BAL-TYPE                    TO ABP-BAL-TYPE.        
    MOVE BPE-BAL-PROGRAM-TYPE            TO ABP-SINGLE-TXN-IND.  
                                                                 
    IF TRANSFER-OUT-TODAY                                        
       SET ABP-TRANSFER-OUT-TODAY-BAL    TO TRUE                 
    ELSE                                                         
       IF  TRANSFER-IN-TODAY                                     
           SET ABP-TRANSFER-IN-TODAY-BAL TO TRUE                 
       ELSE                                                      
           SET ABP-ACTIVE-BAL            TO TRUE                 
       END-IF                                                    
    END-IF.                                                      
                                                                 
    IF ABP-DTE-OPENED EQUAL TO ZERO                              
       MOVE OC-TODAYS-JULIAN             TO ABP-DTE-OPENED       
    END-IF.                                                      
                                                                 
ABPS-SAP1R-XIT.                                                  
    EXIT.                                                        
                                                                 
ABPS-SET-ABP-PART2-RECORD.                                       
    EVALUATE  TCF-GRACE-DAYS-FLAG                                
      WHEN 0                                                     
            MOVE 0                TO ABP-GRACE-OPTION            
      WHEN 1                                                     
            IF  BPE-BAL-TYPE = 'RTL'                             
                MOVE 0            TO ABP-GRACE-OPTION            
            ELSE                                                 
            IF  BPE-BAL-TYPE = 'CSH'                             
                MOVE 1            TO ABP-GRACE-OPTION            
            END-IF                                               
            END-IF                                               
      WHEN 2                                                     
            IF  BPE-BAL-TYPE = 'RTL'                             
                MOVE 1            TO ABP-GRACE-OPTION            
            ELSE                                                 
            IF  BPE-BAL-TYPE = 'CSH'                             
                MOVE 0            TO ABP-GRACE-OPTION            
            END-IF                                               
            END-IF                                               
      WHEN 3                                                     
            MOVE 1                TO ABP-GRACE-OPTION            
    END-EVALUATE.                                                
                                                                 
    IF  BPE-BAL-TYPE = 'RTL'                                     
        MOVE WSP-PROVIS-INTR-SW   TO ABP-PROVIS-FLAG             
    END-IF.                                                      
                                                                 
    MOVE OC-TODAYS-JULIAN         TO ABP-DTE-LST-STAT-CHNG.      
    PERFORM ABPS-RESET-PAID-OUT-PROCESS THRU ABPS-RPOP-XIT       
    MOVE ZERO                     TO ABP-DTE-PAID-OUT            
                                     ABP-DTE-CLOSE               
                                     ABP-DTE-PURGE.              
    MOVE ZERO                     TO ABP-DTE-LST-ACCR.           
    MOVE ZERO                     TO ABP-DTE-ACCR-THRU.          
                                                                 
    MOVE ZERO                     TO ABP-INTR-PER-DIEM.          
                                                                 
    MOVE ZERO                     TO ABP-CHANGE-IND.             
    MOVE ZERO                     TO ABP-CHANGE-TYPE.            
    MOVE ZERO                     TO ABP-ADJUST-FLAG.            
    MOVE ZERO                     TO ABP-CEILING.                
    MOVE ZERO                     TO ABP-FLOOR.                  
    MOVE ZERO                     TO ABP-RATE-1.                 
    MOVE ZERO                     TO ABP-RATE-2.                 
    MOVE ZERO                     TO ABP-RATE-3.                 
    MOVE ZERO                     TO ABP-LIMIT-1.                
    MOVE ZERO                     TO ABP-LIMIT-2.                
    MOVE ZERO                     TO ABP-LIMIT-OPTION.           
                                                                 
    MOVE WSBP-ABP-CONTROL-RECORD  TO ABP1-RECORD-TBL (ABP1-IDX). 
                                                                 
ABPS-SAP2R-XIT.                                                  
    EXIT.                                                        
                                                                 
ABPS-RESET-PAID-OUT-PROCESS.                                     
    IF  ABP-DTE-PAID-OUT > ZERO AND                              
       (ABP-DTE-PAID-OUT <= ABP-DTE-LST-ACTIVE OR                
        ABP-DTE-PAID-OUT <= ABP-DTE-LST-DEBIT  OR                
        ABP-DTE-PAID-OUT <= ABP-DTE-LST-CREDIT OR                
        ABP-DISPUTED-BAL NOT EQUAL TO ZERO     OR                
        ABP-BALANCE      NOT EQUAL TO ZERO)                      
        IF  ABP-DTE-PAID-OUT > ZERO                              
            MOVE ABP-DTE-PAID-OUT  TO ABP-DTE-LST-PAID-OUT       
            MOVE ZERO              TO ABP-DTE-PAID-OUT           
                                      ABP-DTE-CLOSE              
                                      ABP-DTE-PURGE              
        END-IF                                                   
    END-IF.                                                      
                                                                 
ABPS-RPOP-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-AUTO-CHGOFF-REVERSAL.                                       
    PERFORM VARYING ABP1-IDX  FROM 1 BY 1                        
            UNTIL ABP1-IDX    IS GREATER THAN ABP1-NO-OF-ENTRY   
       MOVE ABP1-RECORD-TBL (ABP1-IDX)                           
                              TO WSBP-ABP-CONTROL-RECORD         
       PERFORM ABPS-AUTO-CHGOFF-REV-ONE THRU ABPS-ACRO-EXIT      
       MOVE WSBP-ABP-CONTROL-RECORD                              
                              TO ABP1-RECORD-TBL (ABP1-IDX)      
    END-PERFORM.                                                 
                                                                 
ABPS-ACR-EXIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-AUTO-CHGOFF-REV-ONE.                                        
    MOVE ZEROS                TO ABP-AMNT-CHARGE-OFF.            
                                                                 
ABPS-ACRO-EXIT.                                                  
    EXIT.                                                        
                                                                 
ABPS-MIGRATE-CHGOFF-STATUS.                                      
    PERFORM VARYING ABP1-IDX  FROM 1 BY 1                        
            UNTIL ABP1-IDX    IS GREATER THAN ABP1-NO-OF-ENTRY   
       MOVE ABP1-RECORD-TBL (ABP1-IDX)                           
                              TO WSBP-ABP-CONTROL-RECORD         
       PERFORM ABPS-MIGRATE-CHGOFF-ONE THRU ABPS-MCSO-XIT        
       MOVE WSBP-ABP-CONTROL-RECORD                              
                              TO ABP1-RECORD-TBL (ABP1-IDX)      
    END-PERFORM.                                                 
                                                                 
ABPS-MCS-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-MIGRATE-CHGOFF-ONE.                                         
    MOVE ABP-BALANCE          TO ABP-AMNT-CHARGE-OFF.            
                                                                 
ABPS-MCSO-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-RESET-BAL-PROG-FIELDS.                                      
    PERFORM VARYING ABP1-IDX  FROM 1 BY 1                        
            UNTIL ABP1-IDX    IS GREATER THAN ABP1-NO-OF-ENTRY   
       MOVE ABP1-RECORD-TBL (ABP1-IDX)                           
                              TO WSBP-ABP-CONTROL-RECORD         
       PERFORM ABPS-RESET-BAL-PROG-FIELDS-ONE                    
                            THRU ABPS-RBPFO-XIT                  
       MOVE WSBP-ABP-CONTROL-RECORD                              
                              TO ABP1-RECORD-TBL (ABP1-IDX)      
    END-PERFORM.                                                 
                                                                 
ABPS-RBPF-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-RESET-BAL-PROG-FIELDS-ONE.                                  
    IF CPS422-FUNCTION-CODE EQUAL TO +3190                       
       MOVE ZEROES          TO ABP-AGGR-BALANCE                  
       GO TO ABPS-RBPFO-XIT                                      
    END-IF.                                                      
                                                                 
    IF CPS422-FUNCTION-CODE EQUAL TO +3120                       
       GO TO ABPS-RESET-ACCOUNT                                  
    END-IF.                                                      
                                                                 
ABPS-RESET-BAL-PROG-FOR-TOT.                                     
    MOVE ZEROES             TO ABP-IBNP-LST-STMT.                
                                                                 
    MOVE ZEROES             TO ABP-BALANCE                       
                               ABP-PRIN-STMT                     
                               ABP-CURR-MON-BAL                  
                               ABP-FEE-CTD                       
                               ABP-FEE-BNP                       
                               ABP-IBNP                          
                               ABP-ICTD                          
                               ABP-AMNT-CHARGE-OFF               
                               ABP-DISPUTED-BAL                  
                               ABP-DISPUTED-CTD                  
                               ABP-BEG-BALANCE                   
                               ABP-AMNT-PMT-CTD                  
                               ABP-AMNT-PMT-RVS-CTD              
                               ABP-YTD-INTR-BILLED               
                               ABP-ACCRUED-INTR                  
                               ABP-INTR-PER-DIEM                 
                               ABP-PROVIS-BAL                    
                               ABP-PROVIS-INTR                   
                               ABP-NEG-PROVIS-BAL                
                               ABP-NEG-PROVIS-INTR.              
                                                                 
    MOVE ZEROES             TO ABP-AGGR-BALANCE.                 
    GO TO ABPS-RBPFO-XIT.                                        
                                                                 
ABPS-RESET-ACCOUNT.                                              
    IF  CYCLE-ONLY-TODAY                                         
        MOVE ZEROES         TO ABP-AMNT-PMT-CTD                  
        MOVE ZEROES         TO ABP-AMNT-PMT-RVS-CTD              
    ELSE                                                         
        MOVE ABP-BALANCE    TO ABP-BEG-BALANCE                   
        MOVE ZEROES         TO ABP-AMNT-PMT-CTD                  
        MOVE ZEROES         TO ABP-AMNT-PMT-RVS-CTD              
        MOVE ZEROES         TO ABP-AMNT-DB                       
                               ABP-NMBR-DB                       
        MOVE ZEROES         TO ABP-AMNT-CR                       
                               ABP-NMBR-CR                       
        MOVE ZEROES         TO ABP-DISPUTED-CTD                  
    END-IF.                                                      
                                                                 
    MOVE ZEROES             TO ABP-AGGR-BALANCE.                 
                                                                 
ABPS-RBPFO-XIT.                                                  
    EXIT.                                                        
                                                                 
ABPS-PROCESS-CHARGE-OFF.                                         
    SET ABP1-IDX              TO 1.                              
    PERFORM UNTIL ABP1-IDX    IS GREATER THAN ABP1-NO-OF-ENTRY   
       IF ABP1-ACTIVE-BAL (ABP1-IDX) OR                          
          ABP1-PENDING-CANC-BAL (ABP1-IDX)                       
          MOVE ABP1-RECORD-TBL (ABP1-IDX)                        
                              TO WSBP-ABP-CONTROL-RECORD         
          PERFORM ABPS-PROCESS-CHARGE-OFF-ONE                    
                            THRU ABPS-PCOO-XIT                   
          MOVE WSBP-ABP-CONTROL-RECORD                           
                              TO ABP1-RECORD-TBL (ABP1-IDX)      
          SET ABP1-IDX UP BY 1                                   
       ELSE                                                      
          SET ABP1-IDX UP BY 1                                   
       END-IF                                                    
    END-PERFORM.                                                 
                                                                 
ABPS-PCO-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-PROCESS-CHARGE-OFF-ONE.                                     
    MOVE ABP-BALANCE           TO ABP-AMNT-CHARGE-OFF.           
    ADD  ABP-AMNT-CHARGE-OFF   TO ABP-AMNT-CR.                   
    MOVE ZEROES                TO ABP-BALANCE                    
                                  ABP-PRIN-STMT                  
                                  ABP-CURR-MON-BAL               
                                  ABP-FEE-BNP                    
                                  ABP-FEE-CTD                    
                                  ABP-IBNP                       
                                  ABP-ICTD.                      
                                                                 
    MOVE ZEROES                TO ABP-ACCRUED-INTR               
                                  ABP-AGGR-BALANCE.              
                                                                 
    MOVE ZEROES                TO ABP-PROVIS-BAL                 
                                  ABP-PROVIS-INTR                
                                  ABP-NEG-PROVIS-BAL             
                                  ABP-NEG-PROVIS-INTR.           
                                                                 
ABPS-PCOO-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-PROCESS-CO-REVERSAL.                                        
    PERFORM VARYING ABP1-IDX  FROM 1 BY 1                        
            UNTIL ABP1-IDX    IS GREATER THAN ABP1-NO-OF-ENTRY   
       IF ABP1-ACTIVE-BAL (ABP1-IDX) AND                         
          ABP1-AMNT-CHARGE-OFF (ABP1-IDX)  NOT EQUAL TO ZERO     
          MOVE ABP1-RECORD-TBL (ABP1-IDX)                        
                              TO WSBP-ABP-CONTROL-RECORD         
          PERFORM ABPS-PROCESS-CO-REVERSAL-ONE                   
                            THRU ABPS-PCORO-XIT                  
          MOVE WSBP-ABP-CONTROL-RECORD                           
                              TO ABP1-RECORD-TBL (ABP1-IDX)      
        END-IF                                                   
    END-PERFORM.                                                 
                                                                 
ABPS-PCOR-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-PROCESS-CO-REVERSAL-ONE.                                    
    ADD ABP-AMNT-CHARGE-OFF   TO ABP-BALANCE                     
                                 ABP-PRIN-STMT                   
                                 ABP-AMNT-DB.                    
    MOVE ZEROS                TO ABP-AMNT-CHARGE-OFF.            
ABPS-PCORO-XIT.                                                  
    EXIT.                                                        
                                                                 
ABPS-SET-ENDING-CO-DISPLAY.                                      
    PERFORM VARYING ABP1-IDX  FROM 1 BY 1                        
            UNTIL ABP1-IDX    IS GREATER THAN ABP1-NO-OF-ENTRY   
       MOVE ABP1-RECORD-TBL (ABP1-IDX)                           
                              TO WSBP-ABP-CONTROL-RECORD         
       PERFORM ABPS-SET-ENDING-CO-DISPLAY-ONE                    
                            THRU ABPS-SECOO-XIT                  
       MOVE WSBP-ABP-CONTROL-RECORD                              
                              TO ABP1-RECORD-TBL (ABP1-IDX)      
    END-PERFORM.                                                 
                                                                 
ABPS-SECO-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-SET-ENDING-CO-DISPLAY-ONE.                                  
    PERFORM ABPS-RESET-PAID-OUT-PROCESS THRU ABPS-RPOP-XIT.      
                                                                 
    MOVE CM-DTE-LST-ACCR      TO ABP-DTE-LST-ACCR.               
    MOVE CM-DTE-ACCR-THRU     TO ABP-DTE-ACCR-THRU.              
                                                                 
ABPS-SECOO-XIT.                                                  
    EXIT.                                                        
                                                                 
ABPS-COMPUTE-CURR-DUE.                                           
    PERFORM VARYING ABP1-IDX  FROM 1 BY 1                        
            UNTIL ABP1-IDX    IS GREATER THAN ABP1-NO-OF-ENTRY   
       MOVE ABP1-RECORD-TBL (ABP1-IDX)                           
                              TO WSBP-ABP-CONTROL-RECORD         
       PERFORM ABPS-COMPUTE-CURR-DUE-ONE                         
                            THRU ABPS-CCDO-XIT                   
       MOVE WSBP-ABP-CONTROL-RECORD                              
                              TO ABP1-RECORD-TBL (ABP1-IDX)      
    END-PERFORM.                                                 
                                                                 
ABPS-CCD-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-COMPUTE-CURR-DUE-ONE.                                       
    MOVE 'N'               TO WSMP-FOUND1.                       
    PERFORM VARYING X-WSMP FROM 1 BY 1                           
            UNTIL  (X-WSMP IS GREATER THAN WSMP-ABP-PER-TOTAL) OR
                   (WSMP-FOUND1 IS EQUAL TO 'Y')                 
       IF ABP-ACT-BAL-PROGRAM IS EQUAL TO WSMP-PARM-BPID(X-WSMP) 
          COMPUTE ABP-AMNT-CURR-DUE ROUNDED =                    
                 ((ABP-PRIN-STMT     +                           
                  ABP-CURR-MON-BAL  -                            
                  ABP-DISPUTED-BAL) *                            
                 (WSMP-PARM-BPPRIN-PER(X-WSMP) / 100)) +         
                  ABP-FEE-BNP       +                            
                  ABP-FEE-CTD       +                            
                  ABP-IBNP          +                            
                  ABP-ICTD                                       
          MOVE 'Y'                  TO WSMP-FOUND1               
       END-IF                                                    
    END-PERFORM.                                                 
                                                                 
    IF ( X-WSMP IS GREATER THAN WSMP-ABP-PER-TOTAL ) AND         
       ( WSMP-FOUND1 IS NOT EQUAL TO 'Y' )                       
        COMPUTE ABP-AMNT-CURR-DUE ROUNDED =                      
               ((ABP-PRIN-STMT     +                             
                 ABP-CURR-MON-BAL  -                             
                 ABP-DISPUTED-BAL) *                             
                (WSMP-PARM-RTL-PRINC-PER / 100)) +               
                 ABP-FEE-BNP       +                             
                 ABP-FEE-CTD       +                             
                 ABP-IBNP          +                             
                 ABP-ICTD                                        
    END-IF.                                                      
                                                                 
    COMPUTE WSMP-ABP-MINPYMT-AMNT ROUNDED =                      
                                     ABP-AMNT-CURR-DUE           
                                   + WSMP-ABP-MINPYMT-AMNT.      
    COMPUTE WSMP-ABP-RTL-TOTDIS-AMNT ROUNDED =                   
                                     ABP-DISPUTED-BAL            
                                   + WSMP-ABP-RTL-TOTDIS-AMNT.   
                                                                 
ABPS-CCDO-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-BALANCE-BUCKET-CYCLING.                                     
    PERFORM VARYING ABP1-IDX  FROM 1 BY 1                        
            UNTIL ABP1-IDX    IS GREATER THAN ABP1-NO-OF-ENTRY   
       MOVE ABP1-RECORD-TBL (ABP1-IDX)                           
                              TO WSBP-ABP-CONTROL-RECORD         
       PERFORM ABPS-BALANCE-BUCK-CYCLING-ONE                     
                            THRU ABPS-BBCO-XIT                   
       MOVE WSBP-ABP-CONTROL-RECORD                              
                              TO ABP1-RECORD-TBL (ABP1-IDX)      
    END-PERFORM.                                                 
                                                                 
ABPS-BBC-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-BALANCE-BUCK-CYCLING-ONE.                                   
    ADD  ABP-ICTD             TO ABP-IBNP.                       
    ADD  ABP-FEE-CTD          TO ABP-FEE-BNP.                    
    ADD  ABP-CURR-MON-BAL     TO ABP-PRIN-STMT.                  
                                                                 
    MOVE ZEROES               TO ABP-ICTD                        
                                 ABP-FEE-CTD                     
                                 ABP-CURR-MON-BAL.               
                                                                 
ABPS-BBCO-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-ACCUMULATE-PROC.                                            
    MOVE ZEROES               TO WS-ABP-TOT-FEE                  
                                 WS-ABP-TOT-INT                  
                                 WS-ABP-TOT-PRIN-AMT.            
                                                                 
    PERFORM VARYING ABP2-IDX  FROM 1 BY 1                        
            UNTIL   ABP2-IDX  IS GREATER THAN WS-BP-BPID-TOTAL   
       MOVE SPACE             TO ABP2-BPID(ABP2-IDX)             
                                 ABP2-LIMT-NBR(ABP2-IDX)         
       MOVE ZERO              TO ABP2-ABP-BALANCE(ABP2-IDX)      
       MOVE ZERO              TO ABP2-CURR-MON-BAL(ABP2-IDX)     
       MOVE ZERO              TO ABP2-PRIN-STMT(ABP2-IDX)        
    END-PERFORM.                                                 
                                                                 
    MOVE ZEROES               TO WS-RABP-NOR-INSTL-PRIN-STMT     
                                 WS-RABP-NOR-INSTL-CURR-MON      
                                 WS-RABP-NOR-INSTL-FEE-BNP       
                                 WS-RABP-NOR-INSTL-FEE-CTD       
                                 WS-RABP-NOR-INSTL-IBNP          
                                 WS-RABP-NOR-INSTL-ICTD          
                                 WS-RABP-NOR-INSTL-DISPUTED      
                                 WS-RABP-CASH-INSTL-PRIN-STMT    
                                 WS-RABP-CASH-INSTL-CURR-MON     
                                 WS-RABP-CASH-INSTL-FEE-BNP      
                                 WS-RABP-CASH-INSTL-FEE-CTD      
                                 WS-RABP-CASH-INSTL-IBNP         
                                 WS-RABP-CASH-INSTL-ICTD         
                                 WS-RABP-CASH-INSTL-DISPUTED     
                                 WS-RABP-LARGE-INSTL-PRIN-STMT   
                                 WS-RABP-LARGE-INSTL-CURR-MON    
                                 WS-RABP-LARGE-INSTL-FEE-BNP     
                                 WS-RABP-LARGE-INSTL-FEE-CTD     
                                 WS-RABP-LARGE-INSTL-IBNP        
                                 WS-RABP-LARGE-INSTL-ICTD        
                                 WS-RABP-LARGE-INSTL-DISPUTED    
                                 WS-RABP-NOR-PRIN-STMT           
                                 WS-RABP-NOR-CURR-MON            
                                 WS-RABP-NOR-FEE-BNP             
                                 WS-RABP-NOR-FEE-CTD             
                                 WS-RABP-NOR-IBNP                
                                 WS-RABP-NOR-ICTD                
                                 WS-RABP-NOR-DISPUTED            
                                 WS-CABP-NOR-PRIN-STMT           
                                 WS-CABP-NOR-CURR-MON            
                                 WS-CABP-NOR-FEE-BNP             
                                 WS-CABP-NOR-FEE-CTD             
                                 WS-CABP-NOR-IBNP                
                                 WS-CABP-NOR-ICTD                
                                 WS-CABP-NOR-DISPUTED.           
                                                                 
    MOVE 1                    TO ABP2-NO-OF-ENTRY.               
    PERFORM VARYING ABP1-IDX  FROM 1 BY 1                        
            UNTIL   ABP1-IDX  IS GREATER THAN ABP1-NO-OF-ENTRY   
       MOVE ABP1-RECORD-TBL (ABP1-IDX)                           
                              TO WSBP-ABP-CONTROL-RECORD         
       MOVE ABP-ORG-NMBR      TO WSBP-BPE2-INPUT-ORG-NMBR        
       MOVE ABP-ACT-BAL-PROGRAM                                  
                              TO WSBP-BPE2-INPUT-BAL-PROG        
       PERFORM ABPS-SEARCH-BPE2-TABLE       THRU ABPS-SBPE2-XIT  
       PERFORM ABPS-SEARCH-BPE2-ERROR       THRU ABPS-SBPE2E-XIT 
                                                                 
       PERFORM ABPS-ACCUMULATE-BALANCE-PROC THRU ABPS-ACBP-XIT   
       PERFORM ABPS-ACCUMULATE-LIMIT1-PROC  THRU ABPS-ACL1P-XIT  
    END-PERFORM.                                                 
    PERFORM ABPS-ACCUMULATE-LIMIT2-PROC     THRU ABPS-ACL2P-XIT. 
                                                                 
    PERFORM ABPS-ACCU-TOTAL-PROC            THRU ABPS-ATP-XIT.   
                                                                 
ABPS-ACP-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-ACCUMULATE-BALANCE-PROC.                                    
    IF  BPE-BAL-TYPE = 'RTL'                                     
        EVALUATE TRUE                                            
           WHEN  BPE-NORMAL-INSTL-TRANS                          
                 PERFORM ABPS-ACCU-RTL-NOR-INSTL-PROC            
                                       THRU ABPS-ARNIP-XIT       
           WHEN  BPE-CASH-INSTL-TRANS                            
                 PERFORM ABPS-ACCU-RTL-CASH-INSTL-PROC           
                                       THRU ABPS-ARCIP-XIT       
           WHEN  BPE-LARGE-INSTL-TRANS                           
                 PERFORM ABPS-ACCU-RTL-LARGE-INSTL-PROC          
                                       THRU ABPS-ARLIP-XIT       
           WHEN  BPE-NOINSTL-TRANS                               
                 PERFORM ABPS-ACCU-RTL-NORMAL-PROC               
                                       THRU ABPS-ARNOP-XIT       
           WHEN  OTHER                                           
                 CONTINUE                                        
        END-EVALUATE                                             
    ELSE                                                         
    IF  BPE-BAL-TYPE = 'CSH'                                     
        EVALUATE TRUE                                            
           WHEN  BPE-NOINSTL-TRANS                               
                 PERFORM ABPS-ACCU-CASH-NORMAL-PROC              
                                       THRU ABPS-ACNOP-XIT       
           WHEN  OTHER                                           
                 CONTINUE                                        
        END-EVALUATE                                             
    END-IF                                                       
    END-IF.                                                      
                                                                 
ABPS-ACBP-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-ACCUMULATE-LIMIT1-PROC.                                     
    SET ABP2-IDX              TO 1.                              
    SEARCH ABP2-RECORD-TBL                                       
      AT END                                                     
         IF  ABP2-NO-OF-ENTRY > 20                               
             DISPLAY 'CPBABP LIMIT NBR FULL'                     
         ELSE                                                    
             SET ABP2-IDX     TO ABP2-NO-OF-ENTRY                
             MOVE ABP-ACT-BAL-PROGRAM                            
                              TO ABP2-BPID(ABP2-IDX)             
             ADD ABP-BALANCE  TO ABP2-ABP-BALANCE(ABP2-IDX)      
             ADD ABP-CURR-MON-BAL                                
                              TO ABP2-CURR-MON-BAL(ABP2-IDX)     
             ADD ABP-PRIN-STMT                                   
                              TO ABP2-PRIN-STMT(ABP2-IDX)        
             ADD 1            TO ABP2-NO-OF-ENTRY                
         END-IF                                                  
      WHEN  ABP-ACT-BAL-PROGRAM = ABP2-BPID(ABP2-IDX)            
            ADD ABP-BALANCE   TO ABP2-ABP-BALANCE(ABP2-IDX)      
            ADD ABP-CURR-MON-BAL                                 
                              TO ABP2-CURR-MON-BAL(ABP2-IDX)     
            ADD ABP-PRIN-STMT                                    
                              TO ABP2-PRIN-STMT(ABP2-IDX)        
    END-SEARCH.                                                  
                                                                 
ABPS-ACL1P-XIT.                                                  
    EXIT.                                                        
                                                                 
ABPS-ACCUMULATE-LIMIT2-PROC.                                     
    PERFORM VARYING ABP2-IDX  FROM 1 BY 1                        
            UNTIL   ABP2-IDX  IS GREATER THAN ABP2-NO-OF-ENTRY   
      SET BPE2-IDX            TO 1                               
      SEARCH BPE2-RECORD-TABLE                                   
        AT END                                                   
             MOVE WSBP-BAL-LIMT-CUST-NBR-RTL                     
                              TO ABP2-LIMT-NBR (ABP2-IDX)        
        WHEN ABP2-BPID(ABP2-IDX) =                               
                                 BPE2-BAL-PGM-NMBR(BPE2-IDX)     
          IF BPE2-PLAN-LIMIT-CUST-ABP-NMBR(BPE2-IDX) = SPACE OR  
             BPE2-PLAN-LIMIT-CUST-ABP-NMBR(BPE2-IDX) = ZERO  OR  
             BPE2-PLAN-LIMIT-CUST-ABP-NMBR(BPE2-IDX) = LOW-VALUE 
             MOVE WSBP-BAL-LIMT-CUST-NBR-RTL                     
                              TO ABP2-LIMT-NBR (ABP2-IDX)        
          ELSE                                                   
             MOVE BPE2-PLAN-LIMIT-CUST-ABP-NMBR(BPE2-IDX)        
                              TO ABP2-LIMT-NBR (ABP2-IDX)        
          END-IF                                                 
      END-SEARCH                                                 
    END-PERFORM.                                                 
                                                                 
ABPS-ACL2P-XIT.                                                  
    EXIT.                                                        
                                                                 
ABPS-ACCU-RTL-NOR-INSTL-PROC.                                    
    COMPUTE WS-RABP-NOR-INSTL-PRIN-STMT =                        
            WS-RABP-NOR-INSTL-PRIN-STMT +                        
            ABP-PRIN-STMT.                                       
                                                                 
    COMPUTE WS-RABP-NOR-INSTL-CURR-MON  =                        
            WS-RABP-NOR-INSTL-CURR-MON  +                        
            ABP-CURR-MON-BAL.                                    
                                                                 
    COMPUTE WS-RABP-NOR-INSTL-FEE-BNP   =                        
            WS-RABP-NOR-INSTL-FEE-BNP   +                        
            ABP-FEE-BNP.                                         
                                                                 
    COMPUTE WS-RABP-NOR-INSTL-FEE-CTD   =                        
            WS-RABP-NOR-INSTL-FEE-CTD   +                        
            ABP-FEE-CTD.                                         
                                                                 
    COMPUTE WS-RABP-NOR-INSTL-IBNP      =                        
            WS-RABP-NOR-INSTL-IBNP      +                        
            ABP-IBNP.                                            
                                                                 
    COMPUTE WS-RABP-NOR-INSTL-ICTD      =                        
            WS-RABP-NOR-INSTL-ICTD      +                        
            ABP-ICTD.                                            
                                                                 
    COMPUTE WS-RABP-NOR-INSTL-DISPUTED  =                        
            WS-RABP-NOR-INSTL-DISPUTED  +                        
            ABP-DISPUTED-BAL.                                    
                                                                 
ABPS-ARNIP-XIT.                                                  
    EXIT.                                                        
                                                                 
ABPS-ACCU-RTL-CASH-INSTL-PROC.                                   
    COMPUTE WS-RABP-CASH-INSTL-PRIN-STMT =                       
            WS-RABP-CASH-INSTL-PRIN-STMT +                       
            ABP-PRIN-STMT.                                       
                                                                 
    COMPUTE WS-RABP-CASH-INSTL-CURR-MON =                        
            WS-RABP-CASH-INSTL-CURR-MON +                        
            ABP-CURR-MON-BAL.                                    
                                                                 
    COMPUTE WS-RABP-CASH-INSTL-FEE-BNP  =                        
            WS-RABP-CASH-INSTL-FEE-BNP  +                        
            ABP-FEE-BNP.                                         
                                                                 
    COMPUTE WS-RABP-CASH-INSTL-FEE-CTD  =                        
            WS-RABP-CASH-INSTL-FEE-CTD  +                        
            ABP-FEE-CTD.                                         
                                                                 
    COMPUTE WS-RABP-CASH-INSTL-IBNP     =                        
            WS-RABP-CASH-INSTL-IBNP     +                        
            ABP-IBNP.                                            
                                                                 
    COMPUTE WS-RABP-CASH-INSTL-ICTD     =                        
            WS-RABP-CASH-INSTL-ICTD     +                        
            ABP-ICTD.                                            
                                                                 
    COMPUTE WS-RABP-CASH-INSTL-DISPUTED =                        
            WS-RABP-CASH-INSTL-DISPUTED +                        
            ABP-DISPUTED-BAL.                                    
                                                                 
ABPS-ARCIP-XIT.                                                  
    EXIT.                                                        
                                                                 
ABPS-ACCU-RTL-LARGE-INSTL-PROC.                                  
    COMPUTE WS-RABP-LARGE-INSTL-PRIN-STMT =                      
            WS-RABP-LARGE-INSTL-PRIN-STMT +                      
            ABP-PRIN-STMT.                                       
                                                                 
    COMPUTE WS-RABP-LARGE-INSTL-CURR-MON =                       
            WS-RABP-LARGE-INSTL-CURR-MON +                       
            ABP-CURR-MON-BAL.                                    
                                                                 
    COMPUTE WS-RABP-LARGE-INSTL-FEE-BNP =                        
            WS-RABP-LARGE-INSTL-FEE-BNP +                        
            ABP-FEE-BNP.                                         
                                                                 
    COMPUTE WS-RABP-LARGE-INSTL-FEE-CTD =                        
            WS-RABP-LARGE-INSTL-FEE-CTD +                        
            ABP-FEE-CTD.                                         
                                                                 
    COMPUTE WS-RABP-LARGE-INSTL-IBNP    =                        
            WS-RABP-LARGE-INSTL-IBNP    +                        
            ABP-IBNP.                                            
                                                                 
    COMPUTE WS-RABP-LARGE-INSTL-ICTD    =                        
            WS-RABP-LARGE-INSTL-ICTD    +                        
            ABP-ICTD.                                            
                                                                 
    COMPUTE WS-RABP-LARGE-INSTL-DISPUTED =                       
            WS-RABP-LARGE-INSTL-DISPUTED +                       
            ABP-DISPUTED-BAL.                                    
                                                                 
ABPS-ARLIP-XIT.                                                  
    EXIT.                                                        
                                                                 
ABPS-ACCU-RTL-NORMAL-PROC.                                       
    COMPUTE WS-RABP-NOR-PRIN-STMT =                              
            WS-RABP-NOR-PRIN-STMT +                              
            ABP-PRIN-STMT.                                       
                                                                 
    COMPUTE WS-RABP-NOR-CURR-MON  =                              
            WS-RABP-NOR-CURR-MON  +                              
            ABP-CURR-MON-BAL.                                    
                                                                 
    COMPUTE WS-RABP-NOR-FEE-BNP   =                              
            WS-RABP-NOR-FEE-BNP   +                              
            ABP-FEE-BNP.                                         
                                                                 
    COMPUTE WS-RABP-NOR-FEE-CTD   =                              
            WS-RABP-NOR-FEE-CTD   +                              
            ABP-FEE-CTD.                                         
                                                                 
    COMPUTE WS-RABP-NOR-IBNP      =                              
            WS-RABP-NOR-IBNP      +                              
            ABP-IBNP.                                            
                                                                 
    COMPUTE WS-RABP-NOR-ICTD      =                              
            WS-RABP-NOR-ICTD      +                              
            ABP-ICTD.                                            
                                                                 
    COMPUTE WS-RABP-NOR-DISPUTED  =                              
            WS-RABP-NOR-DISPUTED  +                              
            ABP-DISPUTED-BAL.                                    
                                                                 
ABPS-ARNOP-XIT.                                                  
    EXIT.                                                        
                                                                 
ABPS-ACCU-CASH-NORMAL-PROC.                                      
    COMPUTE WS-CABP-NOR-PRIN-STMT =                              
            WS-CABP-NOR-PRIN-STMT +                              
            ABP-PRIN-STMT.                                       
                                                                 
    COMPUTE WS-CABP-NOR-CURR-MON  =                              
            WS-CABP-NOR-CURR-MON  +                              
            ABP-CURR-MON-BAL.                                    
                                                                 
    COMPUTE WS-CABP-NOR-FEE-BNP   =                              
            WS-CABP-NOR-FEE-BNP   +                              
            ABP-FEE-BNP.                                         
                                                                 
    COMPUTE WS-CABP-NOR-FEE-CTD   =                              
            WS-CABP-NOR-FEE-CTD   +                              
            ABP-FEE-CTD.                                         
                                                                 
    COMPUTE WS-CABP-NOR-IBNP      =                              
            WS-CABP-NOR-IBNP      +                              
            ABP-IBNP.                                            
                                                                 
    COMPUTE WS-CABP-NOR-ICTD      =                              
            WS-CABP-NOR-ICTD      +                              
            ABP-ICTD.                                            
                                                                 
    COMPUTE WS-CABP-NOR-DISPUTED  =                              
            WS-CABP-NOR-DISPUTED  +                              
            ABP-DISPUTED-BAL.                                    
                                                                 
ABPS-ACNOP-XIT.                                                  
    EXIT.                                                        
                                                                 
ABPS-ACCU-TOTAL-PROC.                                            
                                                                 
     COMPUTE WS-ABP-TOT-FEE =  WS-RABP-NOR-INSTL-FEE-BNP +       
                               WS-RABP-NOR-INSTL-FEE-CTD +       
                               WS-RABP-CASH-INSTL-FEE-BNP +      
                               WS-RABP-CASH-INSTL-FEE-CTD +      
                               WS-RABP-LARGE-INSTL-FEE-BNP +     
                               WS-RABP-LARGE-INSTL-FEE-CTD +     
                               WS-RABP-NOR-FEE-BNP +             
                               WS-RABP-NOR-FEE-CTD +             
                               WS-CABP-NOR-FEE-BNP +             
                               WS-CABP-NOR-FEE-CTD.              
                                                                 
     COMPUTE WS-ABP-TOT-INT =  WS-RABP-NOR-INSTL-IBNP +          
                               WS-RABP-NOR-INSTL-ICTD +          
                               WS-RABP-CASH-INSTL-IBNP +         
                               WS-RABP-CASH-INSTL-ICTD +         
                               WS-RABP-LARGE-INSTL-IBNP +        
                               WS-RABP-LARGE-INSTL-ICTD +        
                               WS-RABP-NOR-IBNP +                
                               WS-RABP-NOR-ICTD +                
                               WS-CABP-NOR-IBNP +                
                               WS-CABP-NOR-ICTD.                 
                                                                 
     COMPUTE WS-ABP-TOT-PRIN-AMT = WS-RABP-NOR-INSTL-PRIN-STMT + 
                               WS-RABP-NOR-INSTL-CURR-MON +      
                               WS-RABP-CASH-INSTL-PRIN-STMT +    
                               WS-RABP-CASH-INSTL-CURR-MON +     
                               WS-RABP-LARGE-INSTL-PRIN-STMT +   
                               WS-RABP-LARGE-INSTL-CURR-MON +    
                               WS-RABP-NOR-PRIN-STMT +           
                               WS-RABP-NOR-CURR-MON +            
                               WS-CABP-NOR-PRIN-STMT +           
                               WS-CABP-NOR-CURR-MON.             
                                                                 
ABPS-ATP-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-UPDATE-BAL-PROGRAM.                                         
                                                                 
                                                                 
    PERFORM VARYING ABP1-IDX  FROM 1 BY 1                        
            UNTIL   ABP1-IDX  IS GREATER THAN ABP1-NO-OF-ENTRY   
       MOVE ABP1-RECORD-TBL (ABP1-IDX)                           
                              TO WSBP-ABP-CONTROL-RECORD         
       PERFORM ABPS-UPDATE-BAL-PROGRAM-ONE                       
                            THRU ABPS-UBPO-XIT                   
       MOVE WSBP-ABP-CONTROL-RECORD                              
                              TO ABP1-RECORD-TBL (ABP1-IDX)      
    END-PERFORM.                                                 
                                                                 
                                                                 
    MOVE ZERO                 TO ABP1-NO-OF-ENTRY                
                                 ABP2-NO-OF-ENTRY.               
                                                                 
ABPS-UBP-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-UPDATE-BAL-PROGRAM-ONE.                                     
     IF  CLOSED-ACCOUNT                                          
         SET ABP-CLOSED-BAL       TO TRUE                        
     END-IF.                                                     
     IF  ACCOUNT-TO-BE-PURGED                                    
         SET ABP-BAL-TO-BE-PURGED TO TRUE                        
     END-IF.                                                     
     PERFORM ABPS-CIA-WRITE-ABP-FILE THRU ABPS-CWAF-XIT.         
                                                                 
ABPS-UBPO-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-HANDLE-ABP-PROC.                                            
    MOVE CMT-ORGN-NMBR    TO WSBP-ABPTI-ORG-NMBR.                
    MOVE CMT-TYPE         TO WSBP-ABPTI-TYPE.                    
    MOVE CMT-ACCT-NBR     TO WSBP-ABPTI-ACT-NMBR.                
    MOVE CMT-BAL-PROG-ID  TO WSBP-ABPTI-BAL-PROG.                
    SET WSBP-ABP1-GET-OPTION             TO TRUE.                
    PERFORM ABPS-OPERATION-ABP-PROC    THRU ABPS-OAP-XIT.        
                                                                 
    EVALUATE TRUE                                                
       WHEN WSBP-ABP1-FOUND AND ABP-ACTIVE-BAL                   
            SET WSBP-ABP1-RETURN-NORMAL  TO TRUE                 
       WHEN WSBP-ABP1-NOT-FOUND                                  
            MOVE ABP-RECORD-INIT  TO WSBP-ABP-CONTROL-RECORD     
            MOVE CMT-ORGN-NMBR    TO ABP-ORG-NMBR                
            MOVE CMT-TYPE         TO ABP-TYP-NMBR                
            MOVE CMT-ACCT-NBR     TO ABP-ACCOUNT-NMBR            
            MOVE ZERO             TO ABP-ACT-BAL-SEQ-NMBR        
            MOVE CMT-BAL-PROG-ID  TO ABP-ACT-BAL-PROGRAM         
            MOVE CM-TYPE-ID       TO ABP-ASPD-ID                 
            SET WSBP-ABP1-ADD-OPTION     TO TRUE                 
            PERFORM ABPS-OPERATION-ABP-PROC THRU ABPS-OAP-XIT    
            SET WSBP-ABP1-RETURN-ADDED   TO TRUE                 
    END-EVALUATE.                                                
                                                                 
    IF (NOT WSBP-ABP1-RETURN-ERROR)                              
        MOVE ABP-ACT-BAL-SEQ-NMBR TO CMT-BAL-SEQ-NMBR            
    END-IF.                                                      
                                                                 
ABPS-HAP-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-TRANS-BAL-PROG-CHECK.                                       
    MOVE CMT-ORGN-NMBR              TO WSBP-BPE2-INPUT-ORG-NMBR. 
    MOVE CMT-BAL-PROG-ID            TO WSBP-BPE2-INPUT-BAL-PROG. 
    PERFORM ABPS-SEARCH-BPE2-TABLE THRU ABPS-SBPE2-XIT.          
    IF  WSBP-BPE2-NOT-FOUND                                      
        SET NO-BAL-PROG-ID          TO TRUE                      
        GO TO ABPS-TBPC-XIT                                      
    ELSE                                                         
        IF  RETAIL-TRANSACTIONS OR                               
            RETAIL-DISPUTE-REL-CUSTOMER                          
            CONTINUE                                             
        ELSE                                                     
            SET INV-BAL-PROG-ID     TO TRUE                      
            GO TO ABPS-TBPC-XIT                                  
        END-IF                                                   
    END-IF.                                                      
                                                                 
    IF  CMT-BAL-PROG-ID NOT EQUAL TO TCR-BAL-PROG-ID             
        SET UNMATCHED-BAL-PROG-ID   TO TRUE                      
    END-IF.                                                      
                                                                 
ABPS-TBPC-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-PROCESS-SALES.                                              
                                                                 
    PERFORM ABPS-HANDLE-ABP-PROC THRU ABPS-HAP-XIT.              
    PERFORM APPLY-PROVIS-INTR-FOR-DEBITS                         
                                 THRU APIFD-XIT.                 
                                                                 
    ADD CMT-AMNT             TO ABP-BALANCE.                     
    ADD CMT-AMNT             TO ABP-CURR-MON-BAL.                
    ADD CMT-AMNT             TO ABP-AMNT-DB.                     
    ADD 1                    TO ABP-NMBR-DB.                     
                                                                 
    IF ABP-DTE-LST-ACTIVE < OC-TODAYS-JULIAN                     
       MOVE OC-TODAYS-JULIAN TO ABP-DTE-LST-ACTIVE               
    END-IF.                                                      
    IF ABP-DTE-LST-DEBIT  < OC-TODAYS-JULIAN                     
       MOVE OC-TODAYS-JULIAN TO ABP-DTE-LST-DEBIT                
    END-IF.                                                      
                                                                 
    MOVE WSBP-ABP-CONTROL-RECORD  TO ABP1-RECORD-TBL (ABP1-IDX). 
                                                                 
ABPS-PSLS-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-PROCESS-RETURN.                                             
                                                                 
    PERFORM ABPS-HANDLE-ABP-PROC           THRU ABPS-HAP-XIT.    
    PERFORM APPLY-PROVIS-INTR-FOR-CREDITS  THRU  APIFC-XIT.      
                                                                 
    SUBTRACT CMT-AMNT            FROM ABP-BALANCE                
    IF  WS-PRE-STMT-FLAG = 'Y'                                   
        SUBTRACT CMT-AMNT        FROM ABP-PRIN-STMT              
    ELSE                                                         
        SUBTRACT CMT-AMNT        FROM ABP-CURR-MON-BAL           
    END-IF.                                                      
                                                                 
    ADD CMT-AMNT                 TO ABP-AMNT-CR.                 
    ADD 1                        TO ABP-NMBR-CR.                 
                                                                 
    IF ABP-DTE-LST-ACTIVE < OC-TODAYS-JULIAN                     
       MOVE OC-TODAYS-JULIAN     TO ABP-DTE-LST-ACTIVE           
    END-IF.                                                      
    IF ABP-DTE-LST-CREDIT  < OC-TODAYS-JULIAN                    
       MOVE OC-TODAYS-JULIAN     TO ABP-DTE-LST-CREDIT           
    END-IF.                                                      
                                                                 
    MOVE WSBP-ABP-CONTROL-RECORD TO ABP1-RECORD-TBL (ABP1-IDX).  
                                                                 
ABPS-RTN-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-PROCESS-RTL-ADJUST.                                         
                                                                 
    PERFORM ABPS-HANDLE-ABP-PROC THRU ABPS-HAP-XIT.              
                                                                 
    IF NOT RETAIL-DEBIT-ADJUST                                   
       GO TO ABPS-PROCESS-RTL-CREDIT-ADJUST                      
    END-IF.                                                      
                                                                 
ABPS-PROCESS-RTL-DEBIT-ADJUST.                                   
                                                                 
    PERFORM APPLY-PROVIS-INTR-FOR-DEBITS  THRU  APIFD-XIT.       
    ADD CMT-AMNT                 TO ABP-BALANCE.                 
    ADD CMT-AMNT                 TO ABP-CURR-MON-BAL.            
    ADD CMT-AMNT                 TO ABP-AMNT-DB.                 
    ADD 1                        TO ABP-NMBR-DB.                 
                                                                 
    IF ABP-DTE-LST-DEBIT  < OC-TODAYS-JULIAN                     
       MOVE OC-TODAYS-JULIAN     TO ABP-DTE-LST-DEBIT            
    END-IF.                                                      
                                                                 
    GO TO ABPS-PRA-CONTINUE.                                     
                                                                 
ABPS-PROCESS-RTL-CREDIT-ADJUST.                                  
                                                                 
    PERFORM APPLY-PROVIS-INTR-FOR-CREDITS  THRU  APIFC-XIT.      
    SUBTRACT CMT-AMNT            FROM ABP-BALANCE.               
                                                                 
    IF  WS-PRE-STMT-FLAG = 'Y'                                   
        SUBTRACT CMT-AMNT        FROM ABP-PRIN-STMT              
    ELSE                                                         
        SUBTRACT CMT-AMNT        FROM ABP-CURR-MON-BAL           
    END-IF.                                                      
                                                                 
    ADD CMT-AMNT                 TO ABP-AMNT-CR.                 
    ADD 1                        TO ABP-NMBR-CR.                 
                                                                 
    IF ABP-DTE-LST-CREDIT  < OC-TODAYS-JULIAN                    
       MOVE OC-TODAYS-JULIAN     TO ABP-DTE-LST-CREDIT           
    END-IF.                                                      
                                                                 
ABPS-PRA-CONTINUE.                                               
                                                                 
    IF ABP-DTE-LST-ACTIVE < OC-TODAYS-JULIAN                     
       MOVE OC-TODAYS-JULIAN     TO ABP-DTE-LST-ACTIVE           
    END-IF.                                                      
                                                                 
    MOVE WSBP-ABP-CONTROL-RECORD TO ABP1-RECORD-TBL (ABP1-IDX).  
ABPS-PRA-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-PROCESS-ABP-FEES.                                           
                                                                 
    PERFORM ABPS-HANDLE-ABP-PROC THRU ABPS-HAP-XIT.              
                                                                 
    IF NOT RETAIL-SVC-CHRG-DB-ADJUST                             
       GO TO ABPS-PROCESS-ABP-FEES-CREDIT                        
    END-IF.                                                      
                                                                 
ABPS-PROCESS-ABP-FEES-DEBIT.                                     
                                                                 
    ADD CMT-AMNT                 TO ABP-BALANCE.                 
    ADD CMT-AMNT                 TO ABP-FEE-CTD.                 
                                                                 
    ADD CMT-AMNT                 TO ABP-AMNT-DB.                 
    ADD 1                        TO ABP-NMBR-DB.                 
                                                                 
    IF ABP-DTE-LST-DEBIT < OC-TODAYS-JULIAN                      
       MOVE OC-TODAYS-JULIAN     TO ABP-DTE-LST-DEBIT            
    END-IF.                                                      
                                                                 
    GO TO ABPS-PAF-CONTINUE.                                     
                                                                 
ABPS-PROCESS-ABP-FEES-CREDIT.                                    
                                                                 
    SUBTRACT CMT-AMNT            FROM ABP-BALANCE.               
                                                                 
    IF  WS-PRE-STMT-FLAG = 'Y'                                   
        SUBTRACT CMT-AMNT        FROM ABP-FEE-BNP                
    ELSE                                                         
        SUBTRACT CMT-AMNT        FROM ABP-FEE-CTD                
    END-IF.                                                      
                                                                 
    ADD CMT-AMNT                 TO ABP-AMNT-CR.                 
    ADD 1                        TO ABP-NMBR-CR.                 
                                                                 
    IF ABP-DTE-LST-CREDIT  < OC-TODAYS-JULIAN                    
       MOVE OC-TODAYS-JULIAN     TO ABP-DTE-LST-CREDIT           
    END-IF.                                                      
                                                                 
ABPS-PAF-CONTINUE.                                               
                                                                 
    IF ABP-DTE-LST-ACTIVE < OC-TODAYS-JULIAN                     
       MOVE OC-TODAYS-JULIAN     TO ABP-DTE-LST-ACTIVE           
    END-IF.                                                      
                                                                 
    MOVE WSBP-ABP-CONTROL-RECORD TO ABP1-RECORD-TBL (ABP1-IDX).  
ABPS-PAF-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-PROCESS-DISPUTED-ITEMS.                                     
                                                                 
    PERFORM ABPS-HANDLE-ABP-PROC THRU ABPS-HAP-XIT.              
                                                                 
    IF  DISPUTED-RETAIL-ITEM AND                                 
        ABP-PROVIS-INTR-YES  AND                                 
        CMT-EFFECTIVE-DTE GREATER THAN CM-DTE-LST-STMT           
        ADD CMT-AMNT              TO ABP-NEG-PROVIS-BAL          
     END-IF.                                                     
                                                                 
    IF DISPUTED-ITEM                                             
       ADD CMT-AMNT               TO ABP-DISPUTED-BAL            
       IF CMT-EFFECTIVE-DTE GREATER THAN CM-DTE-LST-STMT         
          ADD CMT-AMNT            TO ABP-DISPUTED-CTD            
       END-IF                                                    
    ELSE                                                         
       IF RETAIL-DISPUTE-REL                                     
          SUBTRACT CMT-AMNT FROM ABP-DISPUTED-BAL                
          IF CMT-EFFECTIVE-DTE GREATER THAN CM-DTE-LST-STMT      
             SUBTRACT CMT-AMNT    FROM ABP-DISPUTED-CTD          
          END-IF                                                 
       END-IF                                                    
    END-IF.                                                      
                                                                 
    MOVE WSBP-ABP-CONTROL-RECORD  TO ABP1-RECORD-TBL (ABP1-IDX). 
                                                                 
ABPS-PDI-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-PROCESS-INTR-ADJUST.                                        
                                                                 
    PERFORM ABPS-HANDLE-ABP-PROC THRU ABPS-HAP-XIT.              
                                                                 
    IF NOT RETAIL-INTR-DB-ADJUST                                 
       GO TO ABPS-PROCESS-INTR-CREDIT                            
    END-IF.                                                      
                                                                 
ABPS-PROCESS-INTR-DEBIT.                                         
                                                                 
    ADD CMT-AMNT                 TO ABP-BALANCE.                 
    ADD CMT-AMNT                 TO ABP-ICTD.                    
                                                                 
    ADD CMT-AMNT                 TO ABP-AMNT-DB.                 
    ADD 1                        TO ABP-NMBR-DB.                 
                                                                 
    IF ABP-DTE-LST-DEBIT < OC-TODAYS-JULIAN                      
       MOVE OC-TODAYS-JULIAN     TO ABP-DTE-LST-DEBIT            
    END-IF.                                                      
                                                                 
    GO TO ABPS-PID-CONTINUE.                                     
                                                                 
ABPS-PROCESS-INTR-CREDIT.                                        
                                                                 
    SUBTRACT CMT-AMNT            FROM ABP-BALANCE.               
                                                                 
    IF  WS-PRE-STMT-FLAG = 'Y'                                   
        SUBTRACT CMT-AMNT        FROM ABP-IBNP                   
    ELSE                                                         
        SUBTRACT CMT-AMNT        FROM ABP-ICTD                   
    END-IF.                                                      
                                                                 
    ADD CMT-AMNT                 TO ABP-AMNT-CR.                 
    ADD 1                        TO ABP-NMBR-CR.                 
                                                                 
    IF ABP-DTE-LST-CREDIT  < OC-TODAYS-JULIAN                    
       MOVE OC-TODAYS-JULIAN     TO ABP-DTE-LST-CREDIT           
    END-IF.                                                      
                                                                 
ABPS-PID-CONTINUE.                                               
                                                                 
    IF ABP-DTE-LST-ACTIVE < OC-TODAYS-JULIAN                     
       MOVE OC-TODAYS-JULIAN     TO ABP-DTE-LST-ACTIVE           
    END-IF.                                                      
                                                                 
    MOVE WSBP-ABP-CONTROL-RECORD TO ABP1-RECORD-TBL (ABP1-IDX).  
ABPS-PIA-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-PROCESS-PAID-IN-FULL.                                       
    PERFORM VARYING WSPSB-IDX5  FROM 1 BY 1                      
            UNTIL   WSPSB-IDX5  IS GREATER THAN WS-ABP-BAL-TOTAL 
       MOVE ZERO      TO WSLGR-PYMT-BUCKET-BP-SEQ (WSPSB-IDX5)   
                         WSLGR-PYMT-BUCKET-BP-PRI (WSPSB-IDX5)   
                         WSLGR-PYMT-BUCKET-BP-AMT (WSPSB-IDX5)   
                         WSLGR-PYMT-ALLOC-BP-AMT  (WSPSB-IDX5)   
       MOVE SPACE     TO WSLGR-PYMT-BUCKET-BP-BPID(WSPSB-IDX5)   
    END-PERFORM.                                                 
    MOVE WS-PYMT              TO WSLGR-PYMT.                     
    SET  WSPSB-IDX5           TO 1.                              
    MOVE 1                    TO WSPSB-IDX9.                     
                                                                 
    PERFORM VARYING WSPSB-IDX4 FROM 1 BY 1                       
            UNTIL   WSPSB-IDX4 IS GREATER THAN WS-BASIC-BAL-TOTAL
       IF  WSLGR-PYMT-BUCKET-AMT(WSPSB-IDX4) > ZERO              
           MOVE WSLGR-PYMT-BUCKET-AMT    (WSPSB-IDX4)            
                              TO WSLGR-PYMT-ALLOC-AMT(WSPSB-IDX4)
           SUBTRACT WSLGR-PYMT-BUCKET-AMT(WSPSB-IDX4)            
                            FROM WSLGR-PYMT                      
       ELSE                                                      
           MOVE ZERO          TO WSLGR-PYMT-ALLOC-AMT(WSPSB-IDX4)
       END-IF                                                    
    END-PERFORM.                                                 
                                                                 
    PERFORM VARYING ABP1-IDX  FROM 1 BY 1                        
            UNTIL   ABP1-IDX IS GREATER THAN ABP1-NO-OF-ENTRY    
       MOVE ABP1-RECORD-TBL (ABP1-IDX)                           
                              TO WSBP-ABP-CONTROL-RECORD         
       PERFORM ABPS-PROCESS-PAID-IN-FULL-ONE                     
               THRU ABPS-PPIFO-XIT                               
       MOVE WSBP-ABP-CONTROL-RECORD                              
                              TO ABP1-RECORD-TBL (ABP1-IDX)      
    END-PERFORM.                                                 
                                                                 
    IF WSLGR-PYMT > ZERO                                         
       PERFORM VARYING WSPSB-IDX4 FROM 1 BY 1                    
          UNTIL   WSPSB-IDX4 IS GREATER THAN WS-BASIC-BAL-TOTAL  
          IF  WSLGR-PYMT-BUCKET-SEQ(WSPSB-IDX4) = 002            
              ADD WSLGR-PYMT  TO WSLGR-PYMT-ALLOC-AMT(WSPSB-IDX4)
              MOVE ZERO       TO WSLGR-PYMT                      
          END-IF                                                 
       END-PERFORM                                               
    END-IF.                                                      
                                                                 
ABPS-PPIF-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-PROCESS-PAID-IN-FULL-ONE.                                   
                                                                 
    IF ABP-BALANCE  IS EQUAL TO ZERO                             
       GO TO ABPS-PPIFO-XIT                                      
    END-IF.                                                      
                                                                 
    MOVE ZERO                         TO WSBP-ABP-ALLOC-AMNT.    
    MOVE 'N'                          TO WSBP-ABP-ALLOC-FLAG.    
    PERFORM LOAD-ABP-BAL-INTO-TABLE THRU LABIT-XIT.              
                                                                 
    PERFORM VARYING WSPSB-IDX5 FROM 1 BY 1                       
            UNTIL   WSPSB-IDX5 IS GREATER THAN WS-ABP-BAL-TOTAL  
      IF  WSLGR-PYMT-BUCKET-BP-BPID(WSPSB-IDX5) =                
                                             ABP-ACT-BAL-PROGRAM 
          IF  WSLGR-PYMT-BUCKET-BP-AMT(WSPSB-IDX5) > ZERO        
              MOVE WSLGR-PYMT-BUCKET-BP-AMT    (WSPSB-IDX5)      
                           TO WSLGR-PYMT-ALLOC-BP-AMT(WSPSB-IDX5)
              SUBTRACT WSLGR-PYMT-BUCKET-BP-AMT(WSPSB-IDX5)      
                         FROM WSLGR-PYMT                         
              ADD  WSLGR-PYMT-ALLOC-BP-AMT     (WSPSB-IDX5)      
                           TO WSBP-ABP-ALLOC-AMNT                
              MOVE 'Y'     TO WSBP-ABP-ALLOC-FLAG                
          ELSE                                                   
              MOVE ZERO    TO WSLGR-PYMT-ALLOC-BP-AMT(WSPSB-IDX5)
          END-IF                                                 
      END-IF                                                     
    END-PERFORM.                                                 
                                                                 
    IF  WSBP-ABP-ALLOC-FLAG IS EQUAL TO 'Y'                      
        ADD WSBP-ABP-ALLOC-AMNT  TO ABP-AMNT-PMT-CTD             
                                    ABP-AMNT-CR                  
        ADD 1                    TO ABP-NMBR-CR                  
        MOVE OC-TODAYS-JULIAN    TO ABP-DTE-LST-CREDIT           
                                                                 
        PERFORM VARYING WSPSB-IDX5 FROM 1 BY 1                   
                UNTIL WSPSB-IDX5 IS GREATER THAN WS-ABP-BAL-TOTAL
          PERFORM ABPS-BASE-ABP-BALANCE-PYMT THRU ABPS-BABP-XIT  
        END-PERFORM                                              
    END-IF.                                                      
                                                                 
ABPS-PPIFO-XIT.                                                  
    EXIT.                                                        
                                                                 
ABPS-PROCESS-NOT-PAID-FULL.                                      
    PERFORM VARYING WSPSB-IDX5  FROM 1 BY 1                      
            UNTIL   WSPSB-IDX5  IS GREATER THAN WS-ABP-BAL-TOTAL 
       MOVE ZERO      TO WSLGR-PYMT-BUCKET-BP-SEQ (WSPSB-IDX5)   
                         WSLGR-PYMT-BUCKET-BP-PRI (WSPSB-IDX5)   
                         WSLGR-PYMT-BUCKET-BP-AMT (WSPSB-IDX5)   
                         WSLGR-PYMT-ALLOC-BP-AMT  (WSPSB-IDX5)   
       MOVE SPACE     TO WSLGR-PYMT-BUCKET-BP-BPID(WSPSB-IDX5)   
    END-PERFORM.                                                 
    MOVE WS-PYMT              TO WSLGR-PYMT1.                    
    SET  WSPSB-IDX5           TO 1.                              
    MOVE 1                    TO WSPSB-IDX9.                     
                                                                 
    PERFORM VARYING ABP1-IDX  FROM 1 BY 1                        
            UNTIL   ABP1-IDX IS GREATER THAN ABP1-NO-OF-ENTRY    
       MOVE ABP1-RECORD-TBL (ABP1-IDX)                           
                              TO WSBP-ABP-CONTROL-RECORD         
       PERFORM LOAD-ABP-BAL-INTO-TABLE                           
                            THRU LABIT-XIT                       
       MOVE WSBP-ABP-CONTROL-RECORD                              
                              TO ABP1-RECORD-TBL (ABP1-IDX)      
    END-PERFORM.                                                 
                                                                 
    PERFORM ABPS-LOAD-PRI-INTO-TABLE      THRU ABPS-LPIT-XIT.    
                                                                 
    PERFORM ABPS-LOAD-INTO-BPE4-TABLE     THRU ABPS-LIBT-XIT.    
                                                                 
    PERFORM ABPS-SORT-PYMT-ALLOC-SEQUENCE THRU ABPS-SPAS-XIT.    
                                                                 
    PERFORM ABPS-PYMT-ALLOC-BP-PROCESS    THRU ABPS-PABP-XIT.    
                                                                 
    PERFORM VARYING ABP1-IDX  FROM 1 BY 1                        
            UNTIL   ABP1-IDX IS GREATER THAN ABP1-NO-OF-ENTRY    
       MOVE ZERO              TO WSBP-ABP-ALLOC-AMNT             
       MOVE 'N'               TO WSBP-ABP-ALLOC-FLAG             
       MOVE ABP1-RECORD-TBL (ABP1-IDX)                           
                              TO WSBP-ABP-CONTROL-RECORD         
                                                                 
       PERFORM VARYING WSPSB-IDX5 FROM 1 BY 1                    
         UNTIL   WSPSB-IDX5 IS GREATER THAN WS-ABP-BAL-TOTAL     
         IF  WSLGR-PYMT-BUCKET-BP-BPID(WSPSB-IDX5) =             
                                             ABP-ACT-BAL-PROGRAM 
             IF  WSLGR-PYMT-ALLOC-BP-AMT (WSPSB-IDX5) > ZERO     
                 ADD  WSLGR-PYMT-ALLOC-BP-AMT (WSPSB-IDX5)       
                              TO WSBP-ABP-ALLOC-AMNT             
                 MOVE 'Y'     TO WSBP-ABP-ALLOC-FLAG             
             END-IF                                              
         END-IF                                                  
       END-PERFORM                                               
                                                                 
       IF  WSBP-ABP-ALLOC-FLAG IS EQUAL TO 'Y'                   
           ADD WSBP-ABP-ALLOC-AMNT  TO ABP-AMNT-PMT-CTD          
                                       ABP-AMNT-CR               
           ADD 1                    TO ABP-NMBR-CR               
           MOVE OC-TODAYS-JULIAN    TO ABP-DTE-LST-CREDIT        
                                                                 
           PERFORM VARYING WSPSB-IDX5 FROM 1 BY 1                
            UNTIL   WSPSB-IDX5 IS GREATER THAN WS-ABP-BAL-TOTAL  
            PERFORM ABPS-BASE-ABP-BALANCE-PYMT THRU ABPS-BABP-XIT
           END-PERFORM                                           
       END-IF                                                    
                                                                 
       MOVE WSBP-ABP-CONTROL-RECORD                              
                              TO ABP1-RECORD-TBL (ABP1-IDX)      
    END-PERFORM.                                                 
                                                                 
ABPS-PNPF-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-LOAD-PRI-INTO-TABLE.                                        
    MOVE 'N'          TO WSLGR-BPID-FOUND-FLAG.                  
                                                                 
    PERFORM VARYING WSPSB-IDX4 FROM 1 BY 1                       
            UNTIL   WSPSB-IDX4 IS GREATER THAN WS-BASIC-BAL-TOTAL
      SET WSPSB-IDX6  TO 1                                       
      SEARCH WSPSB-USE-BAL-BUCKETS-GRP                           
         AT END                                                  
            CONTINUE                                             
         WHEN WSLGR-PYMT-BUCKET-SEQ    (WSPSB-IDX4) =            
              WSPSB-USE-BAL-BUCKETS-SER(WSPSB-IDX6)              
            MOVE WSPSB-USE-BAL-BUCKETS-PRI    (WSPSB-IDX6)       
                      TO WSLGR-PYMT-BUCKET-PRI(WSPSB-IDX4)       
      END-SEARCH                                                 
    END-PERFORM.                                                 
                                                                 
    PERFORM VARYING WSPSB-IDX5 FROM 1 BY 1                       
            UNTIL   WSPSB-IDX5 IS GREATER THAN WS-ABP-BAL-TOTAL  
      SET WSPSB-IDX7     TO 1                                    
      SEARCH WSPSB-USE-BAL-BUCKETS-BP-GRP                        
       AT END                                                    
          MOVE 'N'       TO WSLGR-BPID-FOUND-FLAG                
       WHEN WSLGR-PYMT-BUCKET-BP-BPID(WSPSB-IDX5) =              
            WSPSB-USE-BAL-BUCK-BPID  (WSPSB-IDX7)                
          MOVE 'Y'       TO WSLGR-BPID-FOUND-FLAG                
          SET WSPSB-IDX8 TO 1                                    
          SEARCH WSPSB-USE-BAL-BUCK-BP-TABLE                     
           AT END                                                
              CONTINUE                                           
           WHEN WSLGR-PYMT-BUCKET-BP-SEQ (WSPSB-IDX5) =          
                WSPSB-USE-BAL-BUCK-BP-SER(WSPSB-IDX7 WSPSB-IDX8) 
            MOVE WSPSB-USE-BAL-BUCK-BP-PRI(WSPSB-IDX7 WSPSB-IDX8)
                         TO WSLGR-PYMT-BUCKET-BP-PRI(WSPSB-IDX5) 
          END-SEARCH                                             
      END-SEARCH                                                 
                                                                 
      IF  WSLGR-BPID-FOUND-FLAG IS EQUAL TO 'N'                  
          IF  WSLGR-PYMT-BUCKET-BP-SEQ(WSPSB-IDX5) = 001         
              MOVE WSPSB-USE-BAL-BUCKETS-PRI(7)                  
                         TO WSLGR-PYMT-BUCKET-BP-PRI(WSPSB-IDX5) 
          ELSE                                                   
          IF  WSLGR-PYMT-BUCKET-BP-SEQ(WSPSB-IDX5) = 002         
              MOVE WSPSB-USE-BAL-BUCKETS-PRI(8)                  
                         TO WSLGR-PYMT-BUCKET-BP-PRI(WSPSB-IDX5) 
          ELSE                                                   
          IF  WSLGR-PYMT-BUCKET-BP-SEQ(WSPSB-IDX5) = 003         
              MOVE WSPSB-USE-BAL-BUCKETS-PRI(9)                  
                         TO WSLGR-PYMT-BUCKET-BP-PRI(WSPSB-IDX5) 
          ELSE                                                   
          IF  WSLGR-PYMT-BUCKET-BP-SEQ(WSPSB-IDX5) = 004         
              MOVE WSPSB-USE-BAL-BUCKETS-PRI(10)                 
                         TO WSLGR-PYMT-BUCKET-BP-PRI(WSPSB-IDX5) 
          ELSE                                                   
          IF  WSLGR-PYMT-BUCKET-BP-SEQ(WSPSB-IDX5) = 005         
              MOVE WSPSB-USE-BAL-BUCKETS-PRI(11)                 
                         TO WSLGR-PYMT-BUCKET-BP-PRI(WSPSB-IDX5) 
          ELSE                                                   
          IF  WSLGR-PYMT-BUCKET-BP-SEQ(WSPSB-IDX5) = 006         
              MOVE WSPSB-USE-BAL-BUCKETS-PRI(12)                 
                         TO WSLGR-PYMT-BUCKET-BP-PRI(WSPSB-IDX5) 
          END-IF                                                 
          END-IF                                                 
          END-IF                                                 
          END-IF                                                 
          END-IF                                                 
          END-IF                                                 
      END-IF                                                     
    END-PERFORM.                                                 
                                                                 
ABPS-LPIT-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-LOAD-INTO-BPE4-TABLE.                                       
    MOVE ZERO   TO BPE4-NO-OF-ENTRY.                             
    PERFORM VARYING WSPSB-IDX4 FROM 1 BY 1                       
            UNTIL   WSPSB-IDX4 IS GREATER THAN WS-BASIC-BAL-TOTAL
       IF  WSLGR-PYMT-BUCKET-BPID(WSPSB-IDX4) NOT EQUAL TO SPACE 
           ADD 1         TO BPE4-NO-OF-ENTRY                     
           SET BPE4-IDX  TO BPE4-NO-OF-ENTRY                     
                                                                 
           MOVE WSLGR-PYMT-BUCKET-BPID(WSPSB-IDX4)               
                     TO BPE4-BPID (BPE4-IDX)                     
           MOVE 1    TO BPE4-SEQ-NMBR(BPE4-IDX)                  
           MOVE WSLGR-PYMT-BUCKET-SEQ (WSPSB-IDX4)               
                     TO BPE4-BUCKET-SEQ (BPE4-IDX)               
           MOVE WSLGR-PYMT-BUCKET-PRI (WSPSB-IDX4)               
                     TO BPE4-BUCKET-PRI (BPE4-IDX)               
           MOVE WSLGR-PYMT-BUCKET-AMT (WSPSB-IDX4)               
                     TO BPE4-BUCKET-AMT (BPE4-IDX)               
           MOVE WSLGR-PYMT-ALLOC-AMT  (WSPSB-IDX4)               
                     TO BPE4-ALLOC-AMT  (BPE4-IDX)               
       END-IF                                                    
    END-PERFORM.                                                 
                                                                 
    PERFORM VARYING WSPSB-IDX5 FROM 1 BY 1                       
            UNTIL   WSPSB-IDX5 IS GREATER THAN WS-ABP-BAL-TOTAL  
       ADD 1         TO BPE4-NO-OF-ENTRY                         
       SET BPE4-IDX  TO BPE4-NO-OF-ENTRY                         
                                                                 
       MOVE WSLGR-PYMT-BUCKET-BP-BPID(WSPSB-IDX5)                
                     TO BPE4-BPID (BPE4-IDX)                     
       MOVE 1        TO BPE4-SEQ-NMBR(BPE4-IDX)                  
       MOVE WSLGR-PYMT-BUCKET-BP-SEQ (WSPSB-IDX5)                
                     TO BPE4-BUCKET-SEQ (BPE4-IDX)               
       MOVE WSLGR-PYMT-BUCKET-BP-PRI (WSPSB-IDX5)                
                     TO BPE4-BUCKET-PRI (BPE4-IDX)               
       MOVE WSLGR-PYMT-BUCKET-BP-AMT (WSPSB-IDX5)                
                     TO BPE4-BUCKET-AMT (BPE4-IDX)               
       MOVE WSLGR-PYMT-ALLOC-BP-AMT  (WSPSB-IDX5)                
                     TO BPE4-ALLOC-AMT  (BPE4-IDX)               
    END-PERFORM.                                                 
                                                                 
ABPS-LIBT-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-SORT-PYMT-ALLOC-SEQUENCE.                                   
    MOVE LOW-VALUES         TO CPS425-REQUEST-BLOCK.             
    MOVE 0001               TO CPS425-FUNCTION-CODE.             
    MOVE ZERO               TO CPS425-RETURN-CODE.               
    MOVE ZERO               TO CPS425-VALID-ENTRY-NMBR.          
    MOVE 'N'                TO WSBP-SORT-REQ-FLAG.               
                                                                 
    SET BPE4-IDX TO 1.                                           
    PERFORM UNTIL BPE4-IDX > BPE4-NO-OF-ENTRY                    
       ADD  1   TO CPS425-VALID-ENTRY-NMBR                       
       MOVE BPE4-BPID      (BPE4-IDX)                            
             TO CPS425-SORT-BPID-NMBR  (CPS425-VALID-ENTRY-NMBR) 
       MOVE BPE4-BUCKET-SEQ(BPE4-IDX)                            
             TO CPS425-SORT-ENTRY-NMBR (CPS425-VALID-ENTRY-NMBR) 
       MOVE BPE4-BUCKET-PRI(BPE4-IDX)                            
             TO CPS425-SORT-PRIORITY   (CPS425-VALID-ENTRY-NMBR) 
       IF CPS425-VALID-ENTRY-NMBR > 1                            
          IF CPS425-SORT-PRIORITY (CPS425-VALID-ENTRY-NMBR)      
             LESS THAN CPS425-SORT-PRIORITY                      
                              (CPS425-VALID-ENTRY-NMBR - 1)      
             SET WSBP-SORT-REQUIRED TO TRUE                      
          END-IF                                                 
       END-IF                                                    
       SET BPE4-IDX UP BY 1                                      
    END-PERFORM.                                                 
    IF NOT WSBP-SORT-REQUIRED                                    
       GO TO ABPS-SPAS-XIT.                                      
                                                                 
    CALL 'CPQSORT' USING CPS425-REQUEST-BLOCK.                   
                                                                 
    IF CPS425-RETURN-CODE NOT EQUAL TO ZERO                      
        MOVE 60015              TO WS-ABEND-CODE                 
        MOVE 'SORT SUBROUTINE CPQSORT ERR'                       
                                TO WS-ABENDMSG                   
        MOVE CPS425-RETURN-CODE TO WS-ABEND-STATUS               
        PERFORM CCSI-ABEND    THRU CCSI-ABEND-EXIT               
    END-IF.                                                      
                                                                 
ABPS-SPAS-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-PYMT-ALLOC-BP-PROCESS.                                      
                                                                 
    MOVE 1             TO WSBP-TEMP-IDX-1.                       
    PERFORM UNTIL (WSBP-TEMP-IDX-1 > CPS425-VALID-ENTRY-NMBR) OR 
                  (WSLGR-PYMT1 <= ZERO)                          
      IF  CPS425-SORT-BPID-NMBR(WSBP-TEMP-IDX-1) = 'RTL ' OR     
                                                   'CSH '        
          SET WSPSB-IDX4 TO 1                                    
          SEARCH WSLGR-PYMT-SEQ-TABLE                            
            AT END                                               
               CONTINUE                                          
            WHEN (WSLGR-PYMT-BUCKET-BPID(WSPSB-IDX4) =           
                  CPS425-SORT-BPID-NMBR (WSBP-TEMP-IDX-1)) AND   
                 (WSLGR-PYMT-BUCKET-SEQ (WSPSB-IDX4) =           
                  CPS425-SORT-ENTRY-NMBR(WSBP-TEMP-IDX-1))       
               IF WSLGR-PYMT-BUCKET-AMT (WSPSB-IDX4) > ZERO      
                  IF WSLGR-PYMT-BUCKET-AMT (WSPSB-IDX4)          
                                         <= WSLGR-PYMT1          
                     MOVE WSLGR-PYMT-BUCKET-AMT(WSPSB-IDX4)      
                          TO WSLGR-PYMT-ALLOC-AMT(WSPSB-IDX4)    
                     SUBTRACT WSLGR-PYMT-BUCKET-AMT(WSPSB-IDX4)  
                          FROM WSLGR-PYMT1                       
                  ELSE                                           
                     MOVE WSLGR-PYMT1                            
                          TO WSLGR-PYMT-ALLOC-AMT(WSPSB-IDX4)    
                     MOVE ZERO                                   
                          TO WSLGR-PYMT1                         
                  END-IF                                         
               ELSE                                              
                  MOVE ZERO                                      
                          TO WSLGR-PYMT-ALLOC-AMT(WSPSB-IDX4)    
               END-IF                                            
          END-SEARCH                                             
      ELSE                                                       
          SET WSPSB-IDX5 TO 1                                    
          SEARCH WSLGR-PYMT-BP-SEQ-TABLE                         
            AT END                                               
               CONTINUE                                          
            WHEN (WSLGR-PYMT-BUCKET-BP-BPID(WSPSB-IDX5) =        
                  CPS425-SORT-BPID-NMBR (WSBP-TEMP-IDX-1)) AND   
                 (WSLGR-PYMT-BUCKET-BP-SEQ (WSPSB-IDX5) =        
                  CPS425-SORT-ENTRY-NMBR(WSBP-TEMP-IDX-1))       
               IF WSLGR-PYMT-BUCKET-BP-AMT (WSPSB-IDX5) > ZERO   
                  IF WSLGR-PYMT-BUCKET-BP-AMT (WSPSB-IDX5)       
                                         <= WSLGR-PYMT1          
                     MOVE WSLGR-PYMT-BUCKET-BP-AMT(WSPSB-IDX5)   
                          TO WSLGR-PYMT-ALLOC-BP-AMT(WSPSB-IDX5) 
                    SUBTRACT WSLGR-PYMT-BUCKET-BP-AMT(WSPSB-IDX5)
                          FROM WSLGR-PYMT1                       
                  ELSE                                           
                     MOVE WSLGR-PYMT1                            
                          TO WSLGR-PYMT-ALLOC-BP-AMT(WSPSB-IDX5) 
                     MOVE ZERO                                   
                          TO WSLGR-PYMT1                         
                  END-IF                                         
               ELSE                                              
                  MOVE ZERO                                      
                          TO WSLGR-PYMT-ALLOC-BP-AMT(WSPSB-IDX5) 
               END-IF                                            
          END-SEARCH                                             
      END-IF                                                     
      ADD  1       TO WSBP-TEMP-IDX-1                            
    END-PERFORM.                                                 
                                                                 
    IF WSLGR-PYMT1 > ZERO                                        
       PERFORM VARYING WSPSB-IDX4 FROM 1 BY 1                    
          UNTIL   WSPSB-IDX4 IS GREATER THAN WS-BASIC-BAL-TOTAL  
          IF  WSLGR-PYMT-BUCKET-SEQ(WSPSB-IDX4) = 002            
              ADD WSLGR-PYMT1 TO WSLGR-PYMT-ALLOC-AMT(WSPSB-IDX4)
              MOVE ZERO       TO WSLGR-PYMT1                     
          END-IF                                                 
       END-PERFORM                                               
    END-IF.                                                      
                                                                 
ABPS-PABP-XIT.                                                   
    EXIT.                                                        
                                                                 
LOAD-ABP-BAL-INTO-TABLE.                                         
    SET WSPSB-IDX5     TO WSPSB-IDX9.                            
    MOVE ABP-CURR-MON-BAL                                        
                       TO WSLGR-PYMT-BUCKET-BP-AMT (WSPSB-IDX5). 
    MOVE ABP-ACT-BAL-PROGRAM                                     
                       TO WSLGR-PYMT-BUCKET-BP-BPID(WSPSB-IDX5). 
    MOVE 001           TO WSLGR-PYMT-BUCKET-BP-SEQ (WSPSB-IDX5). 
    ADD  1             TO WSPSB-IDX9.                            
    SET WSPSB-IDX5     TO WSPSB-IDX9.                            
    MOVE ABP-PRIN-STMT                                           
                       TO WSLGR-PYMT-BUCKET-BP-AMT (WSPSB-IDX5). 
    MOVE ABP-ACT-BAL-PROGRAM                                     
                       TO WSLGR-PYMT-BUCKET-BP-BPID(WSPSB-IDX5). 
    MOVE 002           TO WSLGR-PYMT-BUCKET-BP-SEQ (WSPSB-IDX5). 
    ADD  1             TO WSPSB-IDX9.                            
    SET WSPSB-IDX5     TO WSPSB-IDX9.                            
    MOVE ABP-ICTD                                                
                       TO WSLGR-PYMT-BUCKET-BP-AMT (WSPSB-IDX5). 
    MOVE ABP-ACT-BAL-PROGRAM                                     
                       TO WSLGR-PYMT-BUCKET-BP-BPID(WSPSB-IDX5). 
    MOVE 003           TO WSLGR-PYMT-BUCKET-BP-SEQ (WSPSB-IDX5). 
    ADD  1             TO WSPSB-IDX9.                            
    SET WSPSB-IDX5     TO WSPSB-IDX9.                            
    MOVE ABP-IBNP                                                
                       TO WSLGR-PYMT-BUCKET-BP-AMT (WSPSB-IDX5). 
    MOVE ABP-ACT-BAL-PROGRAM                                     
                       TO WSLGR-PYMT-BUCKET-BP-BPID(WSPSB-IDX5). 
    MOVE 004           TO WSLGR-PYMT-BUCKET-BP-SEQ (WSPSB-IDX5). 
    ADD  1             TO WSPSB-IDX9.                            
    SET WSPSB-IDX5     TO WSPSB-IDX9.                            
    MOVE ABP-FEE-CTD                                             
                       TO WSLGR-PYMT-BUCKET-BP-AMT (WSPSB-IDX5). 
    MOVE ABP-ACT-BAL-PROGRAM                                     
                       TO WSLGR-PYMT-BUCKET-BP-BPID(WSPSB-IDX5). 
    MOVE 005           TO WSLGR-PYMT-BUCKET-BP-SEQ (WSPSB-IDX5). 
    ADD  1             TO WSPSB-IDX9.                            
    SET WSPSB-IDX5     TO WSPSB-IDX9.                            
    MOVE ABP-FEE-BNP                                             
                       TO WSLGR-PYMT-BUCKET-BP-AMT (WSPSB-IDX5). 
    MOVE ABP-ACT-BAL-PROGRAM                                     
                       TO WSLGR-PYMT-BUCKET-BP-BPID(WSPSB-IDX5). 
    MOVE 006           TO WSLGR-PYMT-BUCKET-BP-SEQ (WSPSB-IDX5). 
    ADD  1             TO WSPSB-IDX9.                            
                                                                 
LABIT-XIT.                                                       
    EXIT.                                                        
                                                                 
ABPS-BASE-ABP-BALANCE-PYMT.                                      
    IF  WSLGR-PYMT-BUCKET-BP-BPID(WSPSB-IDX5) =                  
                                           ABP-ACT-BAL-PROGRAM   
        IF  WSLGR-PYMT-ALLOC-BP-AMT(WSPSB-IDX5) > ZERO           
            MOVE WSLGR-PYMT-ALLOC-BP-AMT(WSPSB-IDX5) TO WS-PYMT  
            EVALUATE WSLGR-PYMT-BUCKET-BP-SEQ(WSPSB-IDX5)        
              WHEN 001                                           
                   PERFORM PPT-ABP-CURR-MON  THRU PPT-ABP-CM-XIT 
              WHEN 002                                           
                   PERFORM PPT-ABP-STMT-BAL  THRU PPT-ABP-SB-XIT 
              WHEN 003                                           
                   PERFORM PPT-ABP-ICTD      THRU PPT-ABP-IC-XIT 
              WHEN 004                                           
                   PERFORM PPT-ABP-IBNP      THRU PPT-ABP-IB-XIT 
              WHEN 005                                           
                   PERFORM PPT-ABP-FEE-CTD   THRU PPT-ABP-FC-XIT 
              WHEN 006                                           
                   PERFORM PPT-ABP-FEE-BNP   THRU PPT-ABP-FB-XIT 
              WHEN OTHER                                         
                   CONTINUE                                      
            END-EVALUATE                                         
        END-IF                                                   
    END-IF.                                                      
                                                                 
ABPS-BABP-XIT.                                                   
    EXIT.                                                        
                                                                 
PPT-ABP-CURR-MON.                                                
    MOVE WS-PYMT                     TO WS-AMNT.                 
    SUBTRACT WS-AMNT               FROM ABP-CURR-MON-BAL.        
    SUBTRACT WS-AMNT               FROM ABP-BALANCE.             
    SUBTRACT WS-AMNT               FROM WS-PYMT.                 
    IF  ABP-BAL-TYPE = 'RTL'                                     
        ADD WS-AMNT                 TO WS-RTL-PYMT               
        IF (NOT PROVISIONAL-INTR)   AND                          
           (NOT INTR-FROM-STATEMENT (2))                         
            ADD WS-AMNT             TO WS-RTL-NET-PYMT           
        END-IF                                                   
    ELSE                                                         
        ADD WS-AMNT                 TO WS-CASH-PYMT              
        IF  NOT INTR-FROM-STATEMENT(1)                           
            ADD WS-AMNT             TO WS-CASH-NET-PYMT          
        END-IF                                                   
    END-IF.                                                      
    MOVE CMT-CODE                   TO WS-TC-GL-12.              
    IF  ABP-BAL-TYPE = 'RTL'                                     
        SET WSPT-RTL-CURR-MON-BAL   TO TRUE                      
        MOVE 'INS '                 TO WS-BAL-TYPE-GL            
    ELSE                                                         
        SET WSPT-CSH-CURR-MON-BAL   TO TRUE                      
        MOVE 'INS '                 TO WS-BAL-TYPE-GL            
    END-IF.                                                      
    MOVE WS-GL-GEN-COD              TO WS-TC-GL-34.              
    PERFORM GCUG-INDIV-PYMT-AMTS-GL THRU GIPAG-XIT.              
PPT-ABP-CM-XIT.                                                  
    EXIT.                                                        
                                                                 
PPT-ABP-STMT-BAL.                                                
    MOVE WS-PYMT              TO WS-AMNT.                        
    SUBTRACT WS-AMNT        FROM ABP-PRIN-STMT.                  
    SUBTRACT WS-AMNT        FROM ABP-BALANCE.                    
    SUBTRACT WS-AMNT        FROM WS-PYMT.                        
    IF  ABP-BAL-TYPE = 'RTL'                                     
        ADD WS-AMNT           TO WS-RTL-PYMT                     
        ADD WS-AMNT           TO WS-RTL-NET-PYMT                 
    ELSE                                                         
        ADD WS-AMNT           TO WS-CASH-PYMT                    
        ADD WS-AMNT           TO WS-CASH-NET-PYMT                
    END-IF.                                                      
    MOVE CMT-CODE             TO WS-TC-GL-12.                    
    IF  ABP-BAL-TYPE = 'RTL'                                     
        SET WSPT-RTL-PRIN-STMT-BAL  TO TRUE                      
        MOVE 'INS '                 TO WS-BAL-TYPE-GL            
    ELSE                                                         
        SET WSPT-CSH-PRIN-STMT-BAL  TO TRUE                      
        MOVE 'INS '                 TO WS-BAL-TYPE-GL            
    END-IF.                                                      
    MOVE WS-GL-GEN-COD              TO WS-TC-GL-34.              
    PERFORM GCUG-INDIV-PYMT-AMTS-GL THRU GIPAG-XIT.              
PPT-ABP-SB-XIT.                                                  
    EXIT.                                                        
                                                                 
PPT-ABP-ICTD.                                                    
    MOVE WS-PYMT            TO WS-AMNT.                          
    SUBTRACT WS-AMNT      FROM ABP-ICTD.                         
    SUBTRACT WS-AMNT      FROM ABP-BALANCE.                      
    SUBTRACT WS-AMNT      FROM WS-PYMT.                          
    IF  ABP-BAL-TYPE = 'RTL'                                     
        ADD WS-AMNT         TO CM-RTL-YTD-INTR-PAID              
        ADD WS-AMNT         TO WS-RTL-PYMT                       
        IF (NOT PROVISIONAL-INTR)   AND                          
           (NOT WAIVE-INTR-ON-INTR (2))                          
            ADD WS-AMNT     TO WS-RTL-NET-PYMT                   
        END-IF                                                   
    ELSE                                                         
        ADD WS-AMNT         TO CM-CASH-YTD-INTR-PAID             
        ADD WS-AMNT         TO WS-CASH-PYMT                      
        IF  NOT WAIVE-INTR-ON-INTR (1)                           
            ADD WS-AMNT     TO WS-CASH-NET-PYMT                  
        END-IF                                                   
    END-IF.                                                      
    MOVE CMT-CODE           TO WS-TC-GL-12.                      
    IF  ABP-BAL-TYPE = 'RTL'                                     
        SET WSPT-RTL-INTR-CTD       TO TRUE                      
        MOVE 'RTL '                 TO WS-BAL-TYPE-GL            
    ELSE                                                         
        SET WSPT-CSH-INTR-CTD       TO TRUE                      
        MOVE 'CSH '                 TO WS-BAL-TYPE-GL            
    END-IF.                                                      
    MOVE WS-GL-GEN-COD              TO WS-TC-GL-34.              
    PERFORM GCUG-INDIV-PYMT-AMTS-GL THRU GIPAG-XIT.              
PPT-ABP-IC-XIT.                                                  
    EXIT.                                                        
                                                                 
PPT-ABP-IBNP.                                                    
    MOVE WS-PYMT            TO WS-AMNT.                          
    SUBTRACT WS-AMNT      FROM ABP-IBNP.                         
    SUBTRACT WS-AMNT      FROM ABP-BALANCE.                      
    SUBTRACT WS-AMNT      FROM WS-PYMT.                          
    IF  ABP-BAL-TYPE = 'RTL'                                     
        ADD WS-AMNT         TO CM-RTL-YTD-INTR-PAID              
        ADD WS-AMNT         TO WS-RTL-PYMT                       
        IF  NOT WAIVE-INTR-ON-INTR (2)                           
            ADD WS-AMNT     TO WS-RTL-NET-PYMT                   
        END-IF                                                   
    ELSE                                                         
        ADD WS-AMNT         TO CM-CASH-YTD-INTR-PAID             
        ADD WS-AMNT         TO WS-CASH-PYMT                      
        IF  NOT WAIVE-INTR-ON-INTR(1)                            
            ADD WS-AMNT     TO WS-CASH-NET-PYMT                  
        END-IF                                                   
    END-IF.                                                      
    MOVE CMT-CODE           TO WS-TC-GL-12.                      
    IF  ABP-BAL-TYPE = 'RTL'                                     
        SET WSPT-RTL-INTR-BNP       TO TRUE                      
        MOVE 'RTL '                 TO WS-BAL-TYPE-GL            
    ELSE                                                         
        SET WSPT-CSH-INTR-BNP       TO TRUE                      
        MOVE 'CSH '                 TO WS-BAL-TYPE-GL            
    END-IF.                                                      
    MOVE WS-GL-GEN-COD              TO WS-TC-GL-34.              
    PERFORM GCUG-INDIV-PYMT-AMTS-GL THRU GIPAG-XIT.              
PPT-ABP-IB-XIT.                                                  
    EXIT.                                                        
                                                                 
PPT-ABP-FEE-CTD.                                                 
    MOVE WS-PYMT            TO WS-AMNT.                          
    SUBTRACT WS-AMNT      FROM ABP-FEE-CTD.                      
    SUBTRACT WS-AMNT      FROM ABP-BALANCE.                      
    SUBTRACT WS-AMNT      FROM WS-PYMT.                          
    IF  ABP-BAL-TYPE = 'RTL'                                     
        ADD WS-AMNT         TO WS-RTL-PYMT                       
        IF (NOT PROVISIONAL-INTR)   AND                          
           (NOT WAIVE-INTR-ON-SVC-CHRG (2))                      
            ADD WS-AMNT     TO WS-RTL-NET-PYMT                   
        END-IF                                                   
    ELSE                                                         
        ADD WS-AMNT         TO WS-CASH-PYMT                      
        IF  NOT WAIVE-INTR-ON-SVC-CHRG (1)                       
            ADD WS-AMNT     TO WS-CASH-NET-PYMT                  
        END-IF                                                   
    END-IF.                                                      
    MOVE CMT-CODE           TO WS-TC-GL-12.                      
    IF  ABP-BAL-TYPE = 'RTL'                                     
        SET WSPT-RTL-SVC-CTD        TO TRUE                      
        MOVE WS-GL-FEE-TYPE         TO WS-BAL-TYPE-GL            
    ELSE                                                         
        SET WSPT-CSH-SVC-CTD        TO TRUE                      
        MOVE WS-GL-FEE-TYPE         TO WS-BAL-TYPE-GL            
    END-IF.                                                      
    MOVE WS-GL-GEN-COD              TO WS-TC-GL-34.              
    PERFORM GCUG-INDIV-PYMT-AMTS-GL THRU GIPAG-XIT.              
PPT-ABP-FC-XIT.                                                  
    EXIT.                                                        
                                                                 
PPT-ABP-FEE-BNP.                                                 
    MOVE WS-PYMT            TO WS-AMNT.                          
    SUBTRACT WS-AMNT      FROM ABP-FEE-BNP.                      
    SUBTRACT WS-AMNT      FROM ABP-BALANCE.                      
    SUBTRACT WS-AMNT      FROM WS-PYMT.                          
    IF  ABP-BAL-TYPE = 'RTL'                                     
        ADD WS-AMNT         TO WS-RTL-PYMT                       
        IF  NOT WAIVE-INTR-ON-SVC-CHRG (2)                       
            ADD WS-AMNT     TO WS-RTL-NET-PYMT                   
        END-IF                                                   
    ELSE                                                         
        ADD WS-AMNT         TO WS-CASH-PYMT                      
        IF  NOT WAIVE-INTR-ON-SVC-CHRG (1)                       
            ADD WS-AMNT     TO WS-CASH-NET-PYMT                  
        END-IF                                                   
    END-IF.                                                      
    MOVE CMT-CODE           TO WS-TC-GL-12.                      
    IF  ABP-BAL-TYPE = 'RTL'                                     
        SET WSPT-RTL-SVC-BNP        TO TRUE                      
        MOVE WS-GL-FEE-TYPE         TO WS-BAL-TYPE-GL            
    ELSE                                                         
        SET WSPT-CSH-SVC-BNP        TO TRUE                      
        MOVE WS-GL-FEE-TYPE         TO WS-BAL-TYPE-GL            
    END-IF.                                                      
    MOVE WS-GL-GEN-COD              TO WS-TC-GL-34.              
    PERFORM GCUG-INDIV-PYMT-AMTS-GL THRU GIPAG-XIT.              
PPT-ABP-FB-XIT.                                                  
    EXIT.                                                        
                                                                 
ABPS-BALANCING-ACCT-ROUTINE.                                     
    IF  CPS422-FUNCTION-CODE EQUAL TO +6030                      
        PERFORM ABPS-SUMMARY-ABP-BAL-FIELDS  THRU ABPS-SABF-XIT  
    ELSE                                                         
        PERFORM ABPS-ADJUST-ALLOC-BAL-FIELDS THRU ABPS-AABF-XIT  
    END-IF.                                                      
                                                                 
ABPS-BAR-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-SUMMARY-ABP-BAL-FIELDS.                                     
    PERFORM VARYING WSPSB-IDX5  FROM 1 BY 1                      
            UNTIL   WSPSB-IDX5  IS GREATER THAN WS-ABP-BAL-TOTAL 
       MOVE ZERO      TO WSLGR-PYMT-BUCKET-BP-SEQ (WSPSB-IDX5)   
                         WSLGR-PYMT-BUCKET-BP-PRI (WSPSB-IDX5)   
                         WSLGR-PYMT-BUCKET-BP-AMT (WSPSB-IDX5)   
                         WSLGR-PYMT-ALLOC-BP-AMT  (WSPSB-IDX5)   
       MOVE SPACE     TO WSLGR-PYMT-BUCKET-BP-BPID(WSPSB-IDX5)   
    END-PERFORM.                                                 
    MOVE ZERO                 TO WSLGR-ABP-BEG-BAL               
                                 WSLGR-ABP-NEG-BAL.              
    SET  WSPSB-IDX5           TO 1.                              
    MOVE 1                    TO WSPSB-IDX9.                     
                                                                 
    PERFORM VARYING ABP1-IDX  FROM 1 BY 1                        
            UNTIL   ABP1-IDX IS GREATER THAN ABP1-NO-OF-ENTRY    
       MOVE ABP1-RECORD-TBL (ABP1-IDX)                           
                              TO WSBP-ABP-CONTROL-RECORD         
       PERFORM LOAD-ABP-BAL-INTO-TABLE                           
                            THRU LABIT-XIT                       
       PERFORM SUMMARY-ABP-BAL-FIELDS                            
                            THRU SABF-XIT                        
       MOVE WSBP-ABP-CONTROL-RECORD                              
                              TO ABP1-RECORD-TBL (ABP1-IDX)      
    END-PERFORM.                                                 
                                                                 
ABPS-SABF-XIT.                                                   
    EXIT.                                                        
                                                                 
SUMMARY-ABP-BAL-FIELDS.                                          
    IF  ABP-CURR-MON-BAL     IS LESS THAN ZERO                   
        MOVE ABP-CURR-MON-BAL     TO WS-AMNT                     
        SUBTRACT WS-AMNT        FROM ABP-CURR-MON-BAL            
        SUBTRACT WS-AMNT        FROM ABP-BALANCE                 
        IF  ABP-BAL-TYPE = 'RTL'                                 
            ADD WS-AMNT           TO WS-RTL-PYMT                 
        ELSE                                                     
            ADD WS-AMNT           TO WS-CASH-PYMT                
        END-IF                                                   
        ADD WS-AMNT               TO WSLGR-ABP-NEG-BAL           
    ELSE                                                         
    IF  ABP-CURR-MON-BAL     IS GREATER THAN ZERO                
        MOVE ABP-CURR-MON-BAL     TO WS-AMNT                     
        ADD WS-AMNT               TO WSLGR-ABP-BEG-BAL           
    END-IF                                                       
    END-IF.                                                      
    IF  ABP-PRIN-STMT        IS LESS THAN ZERO                   
        MOVE ABP-PRIN-STMT        TO WS-AMNT                     
        SUBTRACT WS-AMNT        FROM ABP-PRIN-STMT               
        SUBTRACT WS-AMNT        FROM ABP-BALANCE                 
        IF  ABP-BAL-TYPE = 'RTL'                                 
            ADD WS-AMNT           TO WS-RTL-PYMT                 
        ELSE                                                     
            ADD WS-AMNT           TO WS-CASH-PYMT                
        END-IF                                                   
        ADD WS-AMNT               TO WSLGR-ABP-NEG-BAL           
    ELSE                                                         
    IF  ABP-PRIN-STMT        IS GREATER THAN ZERO                
        MOVE ABP-PRIN-STMT        TO WS-AMNT                     
        ADD WS-AMNT               TO WSLGR-ABP-BEG-BAL           
    END-IF                                                       
    END-IF.                                                      
    IF  ABP-ICTD             IS LESS THAN ZERO                   
        MOVE ABP-ICTD             TO WS-AMNT                     
        SUBTRACT WS-AMNT        FROM ABP-ICTD                    
        SUBTRACT WS-AMNT        FROM ABP-BALANCE                 
        IF  ABP-BAL-TYPE = 'RTL'                                 
            ADD WS-AMNT           TO WS-RTL-PYMT                 
        ELSE                                                     
            ADD WS-AMNT           TO WS-CASH-PYMT                
        END-IF                                                   
        ADD WS-AMNT               TO WSLGR-ABP-NEG-BAL           
    ELSE                                                         
    IF  ABP-ICTD             IS GREATER THAN ZERO                
        MOVE ABP-ICTD             TO WS-AMNT                     
        ADD WS-AMNT               TO WSLGR-ABP-BEG-BAL           
    END-IF                                                       
    END-IF.                                                      
    IF  ABP-IBNP             IS LESS THAN ZERO                   
        MOVE ABP-IBNP             TO WS-AMNT                     
        SUBTRACT WS-AMNT        FROM ABP-IBNP                    
        SUBTRACT WS-AMNT        FROM ABP-BALANCE                 
        IF  ABP-BAL-TYPE = 'RTL'                                 
            ADD WS-AMNT           TO WS-RTL-PYMT                 
        ELSE                                                     
            ADD WS-AMNT           TO WS-CASH-PYMT                
        END-IF                                                   
        ADD WS-AMNT               TO WSLGR-ABP-NEG-BAL           
    ELSE                                                         
    IF  ABP-IBNP             IS GREATER THAN ZERO                
        MOVE ABP-IBNP             TO WS-AMNT                     
        ADD WS-AMNT               TO WSLGR-ABP-BEG-BAL           
    END-IF                                                       
    END-IF.                                                      
    IF  ABP-FEE-CTD          IS LESS THAN ZERO                   
        MOVE ABP-FEE-CTD          TO WS-AMNT                     
        SUBTRACT WS-AMNT        FROM ABP-FEE-CTD                 
        SUBTRACT WS-AMNT        FROM ABP-BALANCE                 
        IF  ABP-BAL-TYPE = 'RTL'                                 
            ADD WS-AMNT           TO WS-RTL-PYMT                 
        ELSE                                                     
            ADD WS-AMNT           TO WS-CASH-PYMT                
        END-IF                                                   
        ADD WS-AMNT               TO WSLGR-ABP-NEG-BAL           
    ELSE                                                         
    IF  ABP-FEE-CTD          IS GREATER THAN ZERO                
        MOVE ABP-FEE-CTD          TO WS-AMNT                     
        ADD WS-AMNT               TO WSLGR-ABP-BEG-BAL           
    END-IF                                                       
    END-IF.                                                      
    IF  ABP-FEE-BNP          IS LESS THAN ZERO                   
        MOVE ABP-FEE-BNP          TO WS-AMNT                     
        SUBTRACT WS-AMNT        FROM ABP-FEE-BNP                 
        SUBTRACT WS-AMNT        FROM ABP-BALANCE                 
        IF  ABP-BAL-TYPE = 'RTL'                                 
            ADD WS-AMNT           TO WS-RTL-PYMT                 
        ELSE                                                     
            ADD WS-AMNT           TO WS-CASH-PYMT                
        END-IF                                                   
        ADD WS-AMNT               TO WSLGR-ABP-NEG-BAL           
    ELSE                                                         
    IF  ABP-FEE-BNP          IS GREATER THAN ZERO                
        MOVE ABP-FEE-BNP          TO WS-AMNT                     
        ADD WS-AMNT               TO WSLGR-ABP-BEG-BAL           
    END-IF                                                       
    END-IF.                                                      
                                                                 
SABF-XIT.                                                        
    EXIT.                                                        
                                                                 
ABPS-ADJUST-ALLOC-BAL-FIELDS.                                    
    MOVE WS-PYMT              TO WSLGR-PYMT1.                    
                                                                 
    PERFORM ABPS-LOAD-PRI-INTO-TABLE      THRU ABPS-LPIT-XIT.    
                                                                 
    PERFORM ABPS-LOAD-INTO-BPE4-TABLE     THRU ABPS-LIBT-XIT.    
                                                                 
    PERFORM ABPS-SORT-PYMT-ALLOC-SEQUENCE THRU ABPS-SPAS-XIT.    
                                                                 
    PERFORM ABPS-PYMT-ALLOC-BP-PROCESS    THRU ABPS-PABP-XIT.    
                                                                 
    PERFORM VARYING ABP1-IDX  FROM 1 BY 1                        
            UNTIL   ABP1-IDX IS GREATER THAN ABP1-NO-OF-ENTRY    
       MOVE ABP1-RECORD-TBL (ABP1-IDX)                           
                              TO WSBP-ABP-CONTROL-RECORD         
                                                                 
       PERFORM VARYING WSPSB-IDX5 FROM 1 BY 1                    
               UNTIL WSPSB-IDX5 IS GREATER THAN WS-ABP-BAL-TOTAL 
         PERFORM ABPS-BASE-ABP-BALANCE-PYMT THRU ABPS-BABP-XIT   
       END-PERFORM                                               
                                                                 
       MOVE WSBP-ABP-CONTROL-RECORD                              
                              TO ABP1-RECORD-TBL (ABP1-IDX)      
    END-PERFORM.                                                 
                                                                 
ABPS-AABF-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-COMPUTE-RTL-BEARING-BAL.                                    
    MOVE ZERO                   TO WSI-ABP-BALANCE.              
    PERFORM VARYING ABP1-IDX  FROM 1 BY 1                        
            UNTIL ABP1-IDX    IS GREATER THAN ABP1-NO-OF-ENTRY   
       IF ABP1-ACT-RTL-BAL (ABP1-IDX)                            
          IF WAIVE-INTR-ON-INTR (2)                              
             CONTINUE                                            
          ELSE                                                   
            IF (PROVISIONAL-INTR AND BOTH-INTR-ON-INTR (2)) OR   
                INTR-FROM-STATEMENT (2)                          
                ADD ABP1-IBNP (ABP1-IDX) TO WSI-ABP-BALANCE      
            ELSE                                                 
                ADD ABP1-IBNP (ABP1-IDX) TO WSI-ABP-BALANCE      
                ADD ABP1-ICTD (ABP1-IDX) TO WSI-ABP-BALANCE      
            END-IF                                               
          END-IF                                                 
          IF WAIVE-INTR-ON-SVC-CHRG (2)                          
             CONTINUE                                            
          ELSE                                                   
            IF (PROVISIONAL-INTR AND BOTH-INTR-ON-SVC-CHG (2))   
                OR  INTR-FROM-STATEMENT (2)                      
                ADD ABP1-FEE-BNP (ABP1-IDX) TO WSI-ABP-BALANCE   
            ELSE                                                 
                ADD ABP1-FEE-BNP (ABP1-IDX) TO WSI-ABP-BALANCE   
                ADD ABP1-FEE-CTD (ABP1-IDX) TO WSI-ABP-BALANCE   
            END-IF                                               
          END-IF                                                 
          IF PROVISIONAL-INTR OR INTR-FROM-STATEMENT (2)         
              ADD ABP1-PRIN-STMT (ABP1-IDX) TO WSI-ABP-BALANCE   
          ELSE                                                   
              ADD ABP1-PRIN-STMT (ABP1-IDX) TO WSI-ABP-BALANCE   
              ADD ABP1-CURR-MON-BAL (ABP1-IDX) TO WSI-ABP-BALANCE
          END-IF                                                 
       END-IF                                                    
    END-PERFORM.                                                 
ABPS-CRBB-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-COMPUTE-CSH-BEARING-BAL.                                    
    MOVE ZERO                   TO WSI-ABP-BALANCE.              
    PERFORM VARYING ABP1-IDX  FROM 1 BY 1                        
            UNTIL ABP1-IDX    IS GREATER THAN ABP1-NO-OF-ENTRY   
       IF ABP1-ACT-CASH-BAL (ABP1-IDX)                           
          IF WAIVE-INTR-ON-INTR (1)                              
             CONTINUE                                            
          ELSE                                                   
            IF INTR-FROM-STATEMENT (1)                           
                ADD ABP1-IBNP (ABP1-IDX) TO WSI-ABP-BALANCE      
            ELSE                                                 
                ADD ABP1-IBNP (ABP1-IDX) TO WSI-ABP-BALANCE      
                ADD ABP1-ICTD (ABP1-IDX) TO WSI-ABP-BALANCE      
            END-IF                                               
          END-IF                                                 
          IF WAIVE-INTR-ON-SVC-CHRG (1)                          
             CONTINUE                                            
          ELSE                                                   
            IF INTR-FROM-STATEMENT (1)                           
                ADD ABP1-FEE-BNP (ABP1-IDX) TO WSI-ABP-BALANCE   
            ELSE                                                 
                ADD ABP1-FEE-BNP (ABP1-IDX) TO WSI-ABP-BALANCE   
                ADD ABP1-FEE-CTD (ABP1-IDX) TO WSI-ABP-BALANCE   
            END-IF                                               
          END-IF                                                 
          IF INTR-FROM-STATEMENT (1)                             
              ADD ABP1-PRIN-STMT (ABP1-IDX) TO WSI-ABP-BALANCE   
          ELSE                                                   
              ADD ABP1-PRIN-STMT (ABP1-IDX) TO WSI-ABP-BALANCE   
              ADD ABP1-CURR-MON-BAL (ABP1-IDX) TO WSI-ABP-BALANCE
          END-IF                                                 
       END-IF                                                    
    END-PERFORM.                                                 
ABPS-CCBB-XIT.                                                   
    EXIT.                                                        
                                                                 
ABPS-COMPUTE-PROVIS-BAL.                                         
    MOVE ZERO                   TO WSI-ABP-BALANCE.              
    PERFORM VARYING ABP1-IDX  FROM 1 BY 1                        
            UNTIL ABP1-IDX    IS GREATER THAN ABP1-NO-OF-ENTRY   
       IF ABP1-ACT-RTL-BAL (ABP1-IDX)                            
          IF BOTH-INTR-ON-INTR (2)                               
             ADD ABP1-ICTD (ABP1-IDX) TO WSI-ABP-BALANCE         
          END-IF                                                 
          IF BOTH-INTR-ON-SVC-CHG (2)                            
             ADD ABP1-FEE-CTD (ABP1-IDX) TO WSI-ABP-BALANCE      
          END-IF                                                 
          ADD ABP1-CURR-MON-BAL (ABP1-IDX) TO WSI-ABP-BALANCE    
       END-IF                                                    
    END-PERFORM.                                                 
ABPS-CPB-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-WRITE-CPCDL.                                                
    PERFORM VARYING ABP1-IDX  FROM 1 BY 1                        
            UNTIL   ABP1-IDX IS GREATER THAN ABP1-NO-OF-ENTRY    
       MOVE ABP1-RECORD-TBL(ABP1-IDX)                            
                               TO WSBP-ABP-CONTROL-RECORD        
       MOVE ABP-ORG-NMBR       TO WSBP-BPE2-INPUT-ORG-NMBR       
       MOVE ABP-ACT-BAL-PROGRAM                                  
                               TO WSBP-BPE2-INPUT-BAL-PROG       
       PERFORM ABPS-SEARCH-BPE2-TABLE       THRU ABPS-SBPE2-XIT  
       PERFORM ABPS-SEARCH-BPE2-ERROR       THRU ABPS-SBPE2E-XIT 
       MOVE ZEROES             TO WS-PRI-BAL-OFF                 
                                  WS-FEE-OFF                     
                                  WS-TOTAL-IBNP-OFF              
       COMPUTE WS-PRI-BAL-OFF =                                  
            ABP-PRIN-STMT + ABP-CURR-MON-BAL                     
       COMPUTE WS-FEE-OFF     =                                  
            ABP-FEE-BNP   + ABP-FEE-CTD                          
       COMPUTE WS-TOTAL-IBNP-OFF =                               
            ABP-IBNP      + ABP-ICTD                             
       ADD WS-PRI-BAL-OFF      TO WS-PRIN-AMT-GL                 
       ADD WS-TOTAL-IBNP-OFF   TO WS-INTR-AMT-GL                 
       ADD WS-FEE-OFF          TO WS-FEE-AMT-GL                  
       INITIALIZE GPS082-TRANSFER-DEL-RECORD                     
       IF      WS-PRI-BAL-OFF > 0 OR WS-TOTAL-IBNP-OFF > 0 OR    
               WS-FEE-OFF > 0                                    
          MOVE CM-ORG-NMBR TO GPS082-TD-ORG-NMBR                 
          MOVE CM-TYPE TO GPS082-TD-TYPE                         
          MOVE CM-CARD-NMBR TO GPS082-TD-CARD-NMBR               
          MOVE CM-LAST-PRI-BLOCK-CODE TO                         
               GPS082-TD-CARD-PRE-BLOCK-CODE                     
          MOVE CM-BLOCK-CODE TO GPS082-TD-CARD-BLOCK-CODE        
          MOVE WS-082-CUR-ACCT-LEDGER-STATUS TO                  
                                GPS082-TD-ACCT-LEDGER-STATUS     
          MOVE CM-DOMICILE-BRANCH    TO GPS082-TD-BRANCH         
          MOVE WS-PRI-BAL-OFF TO GPS082-TD-CARD-PRI-BAL          
          MOVE WS-TOTAL-IBNP-OFF TO GPS082-TD-CARD-IBNP          
          MOVE WS-FEE-OFF            TO GPS082-TD-CARD-FEE       
          MOVE WS-082-PRV-ACCT-LEDGER-STATUS                     
                                  TO GPS082-TD-PRE-LEDGER-STATUS 
          MOVE CM-CUSTOMER-NMBR      TO GPS082-TD-CST-ID         
          MOVE 'INS '                TO GPS082-TD-BAL-TYPE       
          MOVE SPACES                TO GPS082-TD-BP-ID          
          MOVE SPACES                TO GPS082-TD-BP-USAGE       
          IF (CM-CORP-ACCT-NUMBER NOT =  SPACES) AND             
             (CM-CORP-ACCT-NUMBER NOT =  ZEROS)  AND             
             (CM-CORP-ACCT-NUMBER NOT =  LOW-VALUE)              
              MOVE 'C'               TO GPS082-TD-CUST-TYPE      
          ELSE                                                   
              MOVE 'P'               TO GPS082-TD-CUST-TYPE      
          END-IF                                                 
          MOVE CM-ASTSCRTZ-CNTPR-ID  TO GPS082-ASTSCRTZ-CNTPR-ID 
          MOVE GPS082-TRANSFER-DEL-RECORD TO CPCDL-REC           
          WRITE CPCDL-REC                                        
          IF  WS-CPCDL-STATUS NOT = '00'                         
              MOVE 00341                TO WS-ABEND-CODE         
              MOVE WS-CPCDL-STATUS      TO WS-ABEND-STATUS       
              MOVE 'CPCDL WRITE ERROR'  TO WS-ABENDMSG           
              PERFORM CCSI-ABEND        THRU CCSI-ABEND-EXIT     
          END-IF                                                 
       END-IF                                                    
    END-PERFORM.                                                 
ABPS-WC-XIT.                                                     
    EXIT.                                                        
                                                                 
ABPS-SAVE-ABP-BALANCE.                                           
    PERFORM VARYING WS-PRE-IDX  FROM 1 BY 1                      
            UNTIL   WS-PRE-IDX  IS GREATER THAN WS-BP-ABP-TOTAL  
       MOVE ZERO          TO WS-PRE-ABP-PRIN-STMT   (WS-PRE-IDX) 
                             WS-PRE-ABP-CURR-MON-BAL(WS-PRE-IDX) 
                             WS-PRE-ABP-FEE-BNP     (WS-PRE-IDX) 
                             WS-PRE-ABP-FEE-CTD     (WS-PRE-IDX) 
                             WS-PRE-ABP-IBNP        (WS-PRE-IDX) 
                             WS-PRE-ABP-ICTD        (WS-PRE-IDX) 
    END-PERFORM.                                                 
    SET  WS-PRE-IDX            TO 1.                             
    PERFORM VARYING ABP1-IDX  FROM 1 BY 1                        
            UNTIL   ABP1-IDX IS GREATER THAN ABP1-NO-OF-ENTRY    
       MOVE ABP1-RECORD-TBL (ABP1-IDX)                           
                               TO WSBP-ABP-CONTROL-RECORD        
       MOVE ABP-PRIN-STMT      TO                                
                             WS-PRE-ABP-PRIN-STMT(WS-PRE-IDX)    
       IF ABP-BAL-TYPE  = 'CSH' AND ABP-PRIN-STMT < 0            
           MOVE ZEROS          TO                                
                             WS-PRE-ABP-PRIN-STMT(WS-PRE-IDX)    
       END-IF                                                    
       MOVE ABP-CURR-MON-BAL   TO                                
                          WS-PRE-ABP-CURR-MON-BAL(WS-PRE-IDX)    
       MOVE ABP-FEE-BNP        TO WS-PRE-ABP-FEE-BNP(WS-PRE-IDX) 
       MOVE ABP-FEE-CTD        TO WS-PRE-ABP-FEE-CTD(WS-PRE-IDX) 
       MOVE ABP-IBNP           TO WS-PRE-ABP-IBNP(WS-PRE-IDX)    
       MOVE ABP-ICTD           TO WS-PRE-ABP-ICTD(WS-PRE-IDX)    
       SET WS-PRE-IDX UP BY 1                                    
    END-PERFORM.                                                 
ABPS-SAB-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-ABP-BAL-CHANGE.                                             
    SET  WS-PRE-IDX            TO 1.                             
    PERFORM VARYING ABP1-IDX  FROM 1 BY 1                        
            UNTIL   ABP1-IDX IS GREATER THAN ABP1-NO-OF-ENTRY    
       MOVE ABP1-RECORD-TBL(ABP1-IDX)                            
                               TO WSBP-ABP-CONTROL-RECORD        
       COMPUTE WS-BAL-CHANGED =                                  
            ABP-PRIN-STMT  - WS-PRE-ABP-PRIN-STMT(WS-PRE-IDX) +  
            ABP-CURR-MON-BAL -                                   
                          WS-PRE-ABP-CURR-MON-BAL(WS-PRE-IDX)    
       IF  ABP-BAL-TYPE  = 'RTL'                                 
           MOVE 'INS '         TO WS-BAL-TYPE-GL                 
           IF  WS-BAL-CHANGED >  0                               
               MOVE WSSC-IN-TC-RTL-BAL-DB     TO RC-TRAN-CODE    
               PERFORM GPS110GL-ACCT-BAL-GL THRU                 
                                     GPS110GL-ABG-EXIT           
           END-IF                                                
           IF  WS-BAL-CHANGED <  0                               
               MOVE WSSC-IN-TC-RTL-BAL-CR     TO RC-TRAN-CODE    
               PERFORM GPS110GL-ACCT-BAL-GL THRU                 
                                     GPS110GL-ABG-EXIT           
           END-IF                                                
       END-IF                                                    
       IF  ABP-BAL-TYPE  = 'CSH'                                 
           MOVE 'INS '         TO WS-BAL-TYPE-GL                 
           IF ABP-PRIN-STMT < 0                                  
              COMPUTE WS-BAL-CHANGED =                           
                      WS-BAL-CHANGED - ABP-PRIN-STMT             
           END-IF                                                
           IF  WS-BAL-CHANGED >  0                               
               MOVE WSSC-IN-TC-CSH-BAL-DB     TO RC-TRAN-CODE    
               PERFORM GPS110GL-ACCT-BAL-GL THRU                 
                                     GPS110GL-ABG-EXIT           
           END-IF                                                
           IF  WS-BAL-CHANGED <  0                               
               MOVE WSSC-IN-TC-CSH-BAL-CR     TO RC-TRAN-CODE    
               PERFORM GPS110GL-ACCT-BAL-GL THRU                 
                                     GPS110GL-ABG-EXIT           
           END-IF                                                
       END-IF                                                    
       COMPUTE WS-BAL-CHANGED =                                  
            ABP-FEE-BNP  -     WS-PRE-ABP-FEE-BNP(WS-PRE-IDX) +  
            ABP-FEE-CTD  -     WS-PRE-ABP-FEE-CTD(WS-PRE-IDX)    
       IF  ABP-BAL-TYPE  = 'RTL'                                 
           MOVE WS-GL-FEE-TYPE TO WS-BAL-TYPE-GL                 
           IF  WS-BAL-CHANGED >  0                               
               MOVE WSSC-IN-TC-RTL-SVC-CHG-DB TO RC-TRAN-CODE    
               PERFORM GPS110GL-ACCT-BAL-GL THRU                 
                                     GPS110GL-ABG-EXIT           
           END-IF                                                
           IF  WS-BAL-CHANGED <  0                               
               MOVE WSSC-IN-TC-RTL-SVC-CHG-CR TO RC-TRAN-CODE    
               PERFORM GPS110GL-ACCT-BAL-GL THRU                 
                                     GPS110GL-ABG-EXIT           
           END-IF                                                
       END-IF                                                    
       IF  ABP-BAL-TYPE  = 'CSH'                                 
           MOVE WS-GL-FEE-TYPE TO WS-BAL-TYPE-GL                 
           IF  WS-BAL-CHANGED >  0                               
               MOVE WSSC-IN-TC-CSH-SVC-CHG-DB TO RC-TRAN-CODE    
               PERFORM GPS110GL-ACCT-BAL-GL THRU                 
                                     GPS110GL-ABG-EXIT           
           END-IF                                                
           IF  WS-BAL-CHANGED <  0                               
               MOVE WSSC-IN-TC-CSH-SVC-CHG-CR TO RC-TRAN-CODE    
               PERFORM GPS110GL-ACCT-BAL-GL THRU                 
                                     GPS110GL-ABG-EXIT           
           END-IF                                                
       END-IF                                                    
       COMPUTE WS-BAL-CHANGED =                                  
            ABP-IBNP -  WS-PRE-ABP-IBNP(WS-PRE-IDX) +            
            ABP-ICTD -  WS-PRE-ABP-ICTD(WS-PRE-IDX)              
       IF  ABP-BAL-TYPE  = 'RTL'                                 
           MOVE 'RTL '         TO WS-BAL-TYPE-GL                 
           IF  WS-BAL-CHANGED >  0                               
               MOVE WSSC-IN-TC-RTL-INT-DB     TO RC-TRAN-CODE    
               PERFORM GPS110GL-ACCT-BAL-GL THRU                 
                                     GPS110GL-ABG-EXIT           
           END-IF                                                
           IF  WS-BAL-CHANGED <  0                               
               MOVE WSSC-IN-TC-RTL-INT-CR     TO RC-TRAN-CODE    
               PERFORM GPS110GL-ACCT-BAL-GL THRU                 
                                     GPS110GL-ABG-EXIT           
           END-IF                                                
       END-IF                                                    
       IF  ABP-BAL-TYPE  = 'CSH'                                 
           MOVE 'CSH '         TO WS-BAL-TYPE-GL                 
           IF  WS-BAL-CHANGED >  0                               
               MOVE WSSC-IN-TC-CSH-INT-DB     TO RC-TRAN-CODE    
               PERFORM GPS110GL-ACCT-BAL-GL THRU                 
                                     GPS110GL-ABG-EXIT           
           END-IF                                                
           IF  WS-BAL-CHANGED <  0                               
               MOVE WSSC-IN-TC-CSH-INT-CR     TO RC-TRAN-CODE    
               PERFORM GPS110GL-ACCT-BAL-GL THRU                 
                                     GPS110GL-ABG-EXIT           
           END-IF                                                
       END-IF                                                    
       SET WS-PRE-IDX UP BY 1                                    
    END-PERFORM.                                                 
ABPS-ABC-XIT.                                                    
    EXIT.                                                        
                                                                 
ABPS-CHARGE-OFF-GL.                                              
    COMPUTE WS-AMOUNT-GL =                                       
          ABP-PRIN-STMT + ABP-CURR-MON-BAL                       
    COMPUTE WS-FEE-AMT-GL =                                      
          ABP-FEE-BNP   + ABP-FEE-CTD                            
    COMPUTE WS-IBNP-GL =                                         
          ABP-IBNP      + ABP-ICTD                               
    INITIALIZE GPS082-TRANSFER-DEL-RECORD                        
    MOVE CM-ORG-NMBR TO GPS082-TD-ORG-NMBR                       
    MOVE CM-TYPE TO GPS082-TD-TYPE                               
    MOVE CM-CARD-NMBR TO GPS082-TD-CARD-NMBR                     
    MOVE CM-LAST-PRI-BLOCK-CODE TO                               
         GPS082-TD-CARD-PRE-BLOCK-CODE                           
    MOVE CM-BLOCK-CODE TO GPS082-TD-CARD-BLOCK-CODE              
    MOVE CM-ACCT-LEDGER-STATUS TO                                
                          GPS082-TD-ACCT-LEDGER-STATUS           
    MOVE CM-DOMICILE-BRANCH    TO GPS082-TD-BRANCH               
    MOVE WS-AMOUNT-GL          TO GPS082-TD-CARD-PRI-BAL         
    MOVE WS-IBNP-GL            TO GPS082-TD-CARD-IBNP            
    MOVE WS-FEE-AMT-GL         TO GPS082-TD-CARD-FEE             
    MOVE CM-PREV-ACCT-LEDGER-STATUS                              
                            TO GPS082-TD-PRE-LEDGER-STATUS       
    MOVE CM-CUSTOMER-NMBR      TO GPS082-TD-CST-ID               
    MOVE ABP-BAL-TYPE          TO GPS082-TD-BAL-TYPE             
    MOVE ABP-ACT-BAL-PROGRAM   TO GPS082-TD-BP-ID                
    MOVE BPE-BAL-APP-IND       TO GPS082-TD-BP-USAGE             
    MOVE GPS082-TRANSFER-DEL-RECORD TO CPCDL-REC                 
    WRITE CPCDL-REC                                              
    IF  WS-CPCDL-STATUS NOT = '00'                               
        MOVE 00041                TO WS-ABEND-CODE               
        MOVE WS-CPCDL-STATUS      TO WS-ABEND-STATUS             
        MOVE 'CPCDL WRITE ERROR'  TO WS-ABENDMSG                 
        PERFORM CCSI-ABEND        THRU CCSI-ABEND-EXIT           
    END-IF.                                                      
ABPS-COG-XIT.                                                    
    EXIT.                                                        
