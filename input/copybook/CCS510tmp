CCSI-BACKUP-DATE.
    IF  WS-DTE-PERIOD IS EQUAL TO 'D'
        COMPUTE WS-DTE-DATE-1   =  WS-DTE-JULDAYS - WS-DTE-FREQ
        MOVE ZERO               TO WS-DTE-RESULT
        PERFORM CCSI-BD-LEAP    THRU CCSI-BD-LEAP-EXIT
        MOVE WS-DTE-DATE-1      TO WS-DTE-JULDAYS
        PERFORM CCSI-JUL-TO-GREG THRU CCSI-JTG-EXIT
        GO TO CCSI-BD-RESET-FIELDS.
    PERFORM CCSI-JUL-TO-GREG    THRU CCSI-JTG-EXIT.
    IF  WS-DATE-FORMAT IS EQUAL TO 1
        MOVE WS-DTE-MON         TO WS-DATE-FORMAT-SAVE
        MOVE WS-DTE-DAY         TO WS-DTE-MON
        MOVE WS-DATE-FORMAT-SAVE TO WS-DTE-DAY.
    IF  WS-DTE-PERIOD IS EQUAL TO 'Y'
        SUBTRACT WS-DTE-FREQ    FROM WS-DTE-YEAR
        GO TO CCSI-BD-MONTH-YEAR-CONV.
    COMPUTE WS-DTE-DATE-1       =  WS-DTE-MON - WS-DTE-FREQ.
    PERFORM CCSI-BD-MON-1       THRU CCSI-BD-M1-EXIT
            UNTIL WS-DTE-DATE-1 IS GREATER THAN ZERO.
    MOVE WS-DTE-DATE-1          TO WS-DTE-MON.
CCSI-BD-MONTH-YEAR-CONV.
    PERFORM CCSI-BD-SPECIFIC-DAY THRU CCSI-BD-SD-EXIT.
    IF  WS-DATE-FORMAT IS EQUAL TO 1
        MOVE WS-DTE-MON         TO WS-DATE-FORMAT-SAVE
        MOVE WS-DTE-DAY         TO WS-DTE-MON
        MOVE WS-DATE-FORMAT-SAVE TO WS-DTE-DAY.
    PERFORM CCSI-GREG-TO-JUL THRU CCSI-GTJ-EXIT.
    PERFORM CCSI-JUL-TO-GREG THRU CCSI-JTG-EXIT.
CCSI-BD-RESET-FIELDS.
    MOVE ZERO                   TO WS-DTE-FREQ.
    MOVE 'D'                    TO WS-DTE-PERIOD.
    MOVE ZERO                   TO WS-DTE-DAY-OF-MON.
    GO TO CCSI-BD-EXIT.
CCSI-BD-LEAP.
    ADD WS-DTE-RESULT           TO WS-DTE-DATE-1.
    IF  WS-DTE-DATE-1 IS GREATER THAN ZERO
        GO TO CCSI-BD-LEAP-EXIT.
    SUBTRACT 1                  FROM WS-DTE-JULYR.
    COMPUTE WS-DTE-WORK         =  WS-DTE-JULYR * 250.
    IF  WS-DTE-RMDR IS GREATER THAN ZERO OR
        WS-DTE-SPECIAL-LEAP-YEAR-SKIP
        MOVE 365                TO WS-DTE-RESULT
    ELSE
        MOVE 366                TO WS-DTE-RESULT.
    GO TO CCSI-BD-LEAP.
CCSI-BD-LEAP-EXIT.
    EXIT.
CCSI-BD-SPECIFIC-DAY.
    COMPUTE WS-DTE-WORK         =  WS-DTE-JULYR * 250.           
    IF  WS-DTE-RMDR IS GREATER THAN ZERO OR                      
        WS-DTE-SPECIAL-LEAP-YEAR-SKIP                            
        MOVE 28                 TO WS-DV-MON (2)                 
    ELSE                                                         
        MOVE 29                 TO WS-DV-MON (2).                
    MOVE WS-DV-MON (WS-DTE-MM)  TO WS-DTE-DATE-1.                
    COMPUTE WS-DTE-WORK         =  WS-DTE-YEAR * 250.
    IF  WS-DTE-RMDR IS GREATER THAN ZERO OR
        WS-DTE-SPECIAL-LEAP-YEAR-SKIP
        MOVE 28                 TO WS-DV-MON (2)
    ELSE
        MOVE 29                 TO WS-DV-MON (2).
    IF  WS-DTE-DAY-OF-MON IS GREATER THAN ZERO AND
        WS-DTE-DAY-OF-MON IS LESS THAN 32
        IF  WS-DTE-DAY-OF-MON IS GREATER THAN
                            WS-DV-MON (WS-DTE-MON)
            MOVE WS-DV-MON (WS-DTE-MON) TO WS-DTE-DAY
            GO TO CCSI-BD-SD-EXIT
        ELSE
            MOVE WS-DTE-DAY-OF-MON TO WS-DTE-DAY
            GO TO CCSI-BD-SD-EXIT.
    IF  WS-DTE-DAY-OF-MON IS EQUAL TO 99
        IF  WS-DTE-DAY IS NOT LESS THAN WS-DTE-DATE-1            
            MOVE WS-DV-MON (WS-DTE-MON) TO WS-DTE-DAY
            GO TO CCSI-BD-SD-EXIT.
    IF  WS-DTE-DAY IS GREATER THAN WS-DV-MON (WS-DTE-MON)
        MOVE WS-DV-MON (WS-DTE-MON) TO WS-DTE-DAY.
CCSI-BD-SD-EXIT.
    EXIT.
CCSI-BD-MON-1.
    IF  WS-DTE-DATE-1 IS LESS THAN 1
        SUBTRACT 1              FROM WS-DTE-YEAR
        ADD 12                  TO WS-DTE-DATE-1.
CCSI-BD-M1-EXIT.
    EXIT.
CCSI-BD-EXIT.
    EXIT.
